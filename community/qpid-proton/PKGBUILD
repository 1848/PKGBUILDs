# Maintainer: Nicola Squartini <tensor5@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable LTO
#  - upstream patch to fix 32-bit FTBFS

pkgname=qpid-proton
pkgver=0.32.0
pkgrel=2
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
depends=('libjsoncpp.so' 'python')
makedepends=('cmake' 'doxygen' 'go' 'python-setuptools' 'python-sphinx' 'python-wheel' 'swig')
source=("https://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz"
        '0001-PROTON-2268-Fix-the-fix-so-it-works-everywhere.patch')
sha512sums=('4d1265308b685d5cdd28fd8746f13704c6e36a8dc35a2c121e07b9db21cabaf440da8dd1cf76892fc366845e7f472af2c29ae94dedee19d53756bba2bd4470b6'
            '3f7cb1da9609e2af1662c5df7c3c40fd2650d5ed1a9baf96aedbb6004bf4bf6dcb8d1003e9eebf4c1e850285f6d7e2994b226864fa2a1b17dd8268f1a493a80c')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../0001-PROTON-2268-Fix-the-fix-so-it-works-everywhere.patch
}

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        -DENABLE_LINKTIME_OPTIMIZATION=OFF \
        -DENABLE_TOX_TEST=OFF \
        ..
    make
}

check() {
    cd ${pkgname}-${pkgver}/build

    make test
}

package() {
    cd ${pkgname}-${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
