# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Peter Reschenhofer <peter.reschenhofer@gmail.com>

# ALARM: Andrea Scarpino <andrea@archlinux.org>
#  - disable documentation build

_pkgauthor=rfjakob
pkgname=gocryptfs
pkgver=2.0.1
pkgrel=1
pkgdesc='Encrypted overlay filesystem written in Go.'
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${pkgname}"
license=('MIT')
depends=('gcc-libs' 'openssl' 'fuse')
makedepends=('go')
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz"
    "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz.asc"
)
sha256sums=('31be3f3a9400bd5eb8a4d5f86f7aee52a488207e12d312f2601ae08e7e26dd02'
            'SKIP')
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740')

build() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"

    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -mod=vendor"
    ./build.bash
}

package() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs
    install -Dm755 -t "${pkgdir}/usr/bin/" gocryptfs-xray/gocryptfs-xray
    install -Dm644 -t "${pkgdir}/usr/share/licenses/gocryptfs" LICENSE
}
