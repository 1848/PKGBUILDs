# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM
#  - set -DCONFIG_SSE=OFF

pkgname=qtractor
pkgver=0.9.18
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="https://qtractor.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'gcc-libs' 'hicolor-icon-theme' 'libmad' 'qt5-base'
'qt5-x11extras' 'zlib')
makedepends=('alsa-lib' 'aubio' 'cmake' 'dssi' 'jack' 'ladspa' 'liblo' 'libogg'
'libsamplerate' 'libsndfile' 'libvorbis' 'lilv' 'lv2' 'qt5-tools' 'rubberband'
'suil' 'vst3sdk')
optdepends=('new-session-manager: for session management')
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host' 'vst3-host')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '04-q_atomic_ftbfs.patch')
sha512sums=('e488f95becde10117fc675a5b20b66378e8ae1f8c388fb12042cafa1f4434292d4156f5fe613b93e0c7409c414df9bf0e614afce843e0a42e4a1bce7c9ee9125'
            'fe0e4d6adcd0eb64c39a194f16c026e6505d5f3b0537b7724ed9060443340559cc12a57665aec3e2a4d4c71ea05b1e575508cae334a8ca3a16543b5fdf55b4bf')
b2sums=('269d2fd9cdb3171155ba32d7af5d6c0ad0037287311d2e77efb706820071fde353c84ba6958c0c0fb75f3ecc22eea5e5f7204018d1503bbfb424f48aefc60150'
        'ab86331b8cdc583cbe740c23e2a47280205cd20e80f23de1d8e189d6f2ea9a651caad14188656e5f85357cb444f9a51544871b2603ad54748db555ab6a9f4124')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i ../04-q_atomic_ftbfs.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_SSE=OFF \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libaubio.so' 'libjack.so' 'liblilv-0.so' 'liblo.so'
  'libogg.so' 'librubberband.so' 'libsamplerate.so' 'libsndfile.so'
  'libsuil-0.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libvorbis.so' )
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  # docs
  install -vDm 644 {AUTHORS,README,README.VST,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
