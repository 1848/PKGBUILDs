# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on ARM

pkgname=qtractor
pkgver=0.9.11
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'aubio' 'desktop-file-utils' 'glibc' 'gcc-libs'
'libsndfile' 'libjack.so' 'liblo' 'libmad' 'libogg' 'libvorbis' 'lilv'
'qt5-x11extras' 'rubberband' 'suil' 'zlib')
makedepends=('qt5-tools' 'ladspa' 'dssi')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '04-q_atomic_ftbfs.patch')
sha512sums=('d85b69763304464e0f75c39d1b91fdadd4333d4bbf9f44c3eb2f4d1ef64bf329fb2c605a7eae70076c3e4bb97b00b6fe9728a022e8aba10272d946578eaebda2'
            'fe0e4d6adcd0eb64c39a194f16c026e6505d5f3b0537b7724ed9060443340559cc12a57665aec3e2a4d4c71ea05b1e575508cae334a8ca3a16543b5fdf55b4bf')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  patch -p1 -i ../04-q_atomic_ftbfs.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
