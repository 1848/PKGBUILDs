# $Id$
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use -D_FILE_OFFSET_BITS=64 with 32-bit ARM

pkgname=sshfs
pkgver=3.3.0
pkgrel=2
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=(x86_64 i686)
url="http://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
makedepends=('meson' 'python-docutils')
source=(https://github.com/libfuse/sshfs/releases/download/sshfs-$pkgver/sshfs-$pkgver.tar.xz{,.asc})
sha256sums=('9ddfcc2564fafe002706b093b6295afbcb2cc0a3bc52bc805cc5361cb69ca51d'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == 'i686' || $CARCH == 'arm' || $CARCH == 'armv6h' || $CARCH == 'armv7h' ]] && export CFLAGS+=" -D_FILE_OFFSET_BITS=64"
  rm -rf build
  meson setup --prefix=/usr --sbindir=bin . build
  cd build
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
