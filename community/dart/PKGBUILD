# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Jacob Bang <julemand101gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added source and sha512 for ARMv7h, AArch64 builds of Dart
#  - configure paths and install symlinks for binaries shipped for arm

buildarch=12

pkgname=dart
pkgver=2.13.0
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://www.dartlang.org/'
depends=('bash')
license=('BSD')
makedepends=('setconf')
options=('!strip')
source_x86_64+=("$pkgname-$pkgver-64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-x64-release.zip")
source_armv7h+=("$pkgname-$pkgver-arm.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm-release.zip")
source_aarch64+=("$pkgname-$pkgver-arm64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm64-release.zip")
sha512sums_x86_64=('93ca82430105bd9996c46819a9c0395d263f8d36b088e02bb3a7a721a43170ebeb4f0f1db01d3fab3030f765c3b2e6e9e15520275b7f45854cda93b45d273ce8')
sha512sums_armv7h=('934d504735d0c8fba4d9114b18084e7859aedcfd62106bfc733df473a48a2654adca2f493cc020d54d423b2729dcb3b29dccda6bbf3e39856e19c35537af18be')
sha512sums_aarch64=('ae676d9157c7927aa5852601f48614c21438e20a4cba4bf34db990abdd2d2c1a14a4de6f255765549166859548725158b70a8072c65d7779e0e1e46ad6fce364')

prepare() {
  # Fix permissions
  find "$pkgname-sdk" -type d -exec chmod a+rx '{}' + \
    -or -type f -exec chmod a+r '{}' +

  cd "$pkgname-sdk/bin"

  # Configure paths
  for _f in dartanalyzer pub dartfmt dartdoc; do
    setconf $_f BIN_DIR "/opt/$pkgname-sdk/bin"
    setconf $_f PROG_NAME "/opt/$pkgname-sdk/bin/$_f"
    setconf $_f SDK_DIR "/opt/$pkgname-sdk/"
  done

  # Extract license (AUTHORS and LICENSE files are missing)
  head -n5 "../include/dart_api.h" > ../../LICENSE
}

package() {
  # Create directories
  install -d "$pkgdir"{"/opt/$pkgname-sdk",/usr/{bin,"share/doc/$pkgname-sdk"}}

  # Package the files
  cp -a "$pkgname-sdk/"* "$pkgdir/opt/$pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartanalyzer pub dartfmt dartdoc dart2native dartaotruntime; do
    ln -s "/opt/$pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package samples and documentation
  for f in samples about.html about_files; do
    echo mv "$pkgdir/opt/$pkgname-sdk/$f" "$pkgdir/usr/share/doc/$pkgname/"
  done

  # BSD License
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
