# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Jacob Bang <julemand101gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added source and sha512 for ARMv7h, AArch64 builds of Dart
#  - configure paths and install symlinks for binaries shipped for arm

buildarch=12

pkgname=dart
pkgver=2.8.3
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://www.dartlang.org/'
depends=('bash')
license=('BSD')
makedepends=('setconf')
options=('!strip')
source_x86_64+=("$pkgname-$pkgver-64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-x64-release.zip")
source_armv7h+=("$pkgname-$pkgver-arm.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm-release.zip")
source_aarch64+=("$pkgname-$pkgver-arm64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm64-release.zip")
sha512sums_x86_64=('03f540cac8972b346195c28efcc8c2c6c26385b7454a8af0e5c25a6312e54095dacf5b016def5d0d0b19c40495edb7c0cc989a306a9e995376c4ba9f80a5e1c7')
sha512sums_armv7h=('3d895fc16b1e2ffba012f7a46952b16e308568370f0ed59c985ede106ba706d38fa023dccc17defaced8feaccfa540263d52f467683ab2263a733f89502e941a')
sha512sums_aarch64=('6c22dc2e84eb8bd54827a3fa975b992baa90185ae4e72d22acb14f82f59326152df9ff1436ced2fef692a34822bc20f3d91487839ac1ca65dcc16df50927be7a')

prepare() {
  # Fix permissions
  find "$pkgname-sdk" -type d -exec chmod a+rx '{}' + \
    -or -type f -exec chmod a+r '{}' +

  cd "$pkgname-sdk/bin"

  # Configure paths
  for _f in dartanalyzer pub dartfmt dartdoc; do
    setconf $_f BIN_DIR "/opt/$pkgname-sdk/bin"
    setconf $_f PROG_NAME "/opt/$pkgname-sdk/bin/$_f"
    setconf $_f SDK_DIR "/opt/$pkgname-sdk/"
  done

  # Extract license (AUTHORS and LICENSE files are missing)
  head -n5 "../include/dart_api.h" > ../../LICENSE
}

package() {
  # Create directories
  install -d "$pkgdir"{"/opt/$pkgname-sdk",/usr/{bin,"share/doc/$pkgname-sdk"}}

  # Package the files
  cp -a "$pkgname-sdk/"* "$pkgdir/opt/$pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartanalyzer pub dartfmt dartdoc dart2native dartaotruntime; do
    ln -s "/opt/$pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package samples and documentation
  for f in samples about.html about_files; do
    echo mv "$pkgdir/opt/$pkgname-sdk/$f" "$pkgdir/usr/share/doc/$pkgname/"
  done

  # BSD License
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
