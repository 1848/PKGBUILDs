# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: T. Jameson Little <t.jameson.little at gmail dot com>
# Contributor: Usagi Ito <usagi@WonderRabbitProject.net>
# Contributor: siasia <http://pastebin.com/qsBEmNCw>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Jacob Bang <julemand101gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - added source and sha512 for ARMv7h, AArch64 builds of Dart
#  - configure paths and install symlinks for binaries shipped for arm

buildarch=12

pkgname=dart
pkgver=2.8.2
pkgrel=1
pkgdesc='The dart programming language SDK'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://www.dartlang.org/'
depends=('bash')
license=('BSD')
makedepends=('setconf')
options=('!strip')
source_x86_64+=("$pkgname-$pkgver-64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-x64-release.zip")
source_armv7h+=("$pkgname-$pkgver-arm.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm-release.zip")
source_aarch64+=("$pkgname-$pkgver-arm64.zip::https://storage.googleapis.com/dart-archive/channels/stable/release/$pkgver/sdk/dartsdk-linux-arm64-release.zip")
sha512sums_x86_64=('6e0f0a47ce64d8a0223e654d8d4bea4dc60090fcd22e12b5dcb8f5cb372354c6d0f967a01fceee58d6069902c01c6765856b60cdf3506f18be06aa182711e947')
sha512sums_armv7h=('e984467343b3a239236b804654b17f97bf21c69d68974f47e42245d6affd64d50e0c918f6c109b888b571009c7f08a6e1fc7fb259f89d76ce452e1269f2ae371')
sha512sums_aarch64=('ed300c52fd2fc6d295bc91a9bb96f30de3dba00a870897fe471ceae813a1c5e15b7396f46b021138ebea3f734446a9529462e3652e7830807bff1606daa86a79')

prepare() {
  # Fix permissions
  find "$pkgname-sdk" -type d -exec chmod a+rx '{}' + \
    -or -type f -exec chmod a+r '{}' +

  cd "$pkgname-sdk/bin"

  # Configure paths
  for _f in dartanalyzer pub dartfmt dartdoc; do
    setconf $_f BIN_DIR "/opt/$pkgname-sdk/bin"
    setconf $_f PROG_NAME "/opt/$pkgname-sdk/bin/$_f"
    setconf $_f SDK_DIR "/opt/$pkgname-sdk/"
  done

  # Extract license (AUTHORS and LICENSE files are missing)
  head -n5 "../include/dart_api.h" > ../../LICENSE
}

package() {
  # Create directories
  install -d "$pkgdir"{"/opt/$pkgname-sdk",/usr/{bin,"share/doc/$pkgname-sdk"}}

  # Package the files
  cp -a "$pkgname-sdk/"* "$pkgdir/opt/$pkgname-sdk/"

  # Set up symbolic links for the executables
  for f in dart dartanalyzer pub dartfmt dartdoc dart2native dartaotruntime; do
    ln -s "/opt/$pkgname-sdk/bin/$f" "$pkgdir/usr/bin/$f"
  done

  # Package samples and documentation
  for f in samples about.html about_files; do
    echo mv "$pkgdir/opt/$pkgname-sdk/$f" "$pkgdir/usr/share/doc/$pkgname/"
  done

  # BSD License
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
