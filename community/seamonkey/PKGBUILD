# Maintainer: Kyle Keen <keenerd@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - mozconfig changes
#  - patch to fix neon usage
#  - patch to fix v7 FTBFS in js

highmem=1
buildarch=12

pkgname=seamonkey
pkgver=2.53.9.1
pkgrel=3
pkgdesc="SeaMonkey internet suite"
arch=('x86_64')
license=('MPL')
depends=('dbus-glib' 'gtk3' 'hunspell' 'icu'
         'libevent' 'libpulse' 'libvpx' 'libxt' 'mime-types'
         'nss' 'sqlite' 'startup-notification')
makedepends=('autoconf2.13' 'clang' 'gtk2' 'imake' 'llvm' 'mesa' 'python2-setuptools'
             'python2-psutil' 'rust' 'yasm' 'unzip' 'zip' 'nasm')
optdepends=('networkmanager: Location detection via available WiFi networks'
	    'libnotify: Notification integration'
	    'pulseaudio: Audio support')
url="https://www.seamonkey-project.org/"
source=("https://archive.mozilla.org/pub/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.xz"
        "clang-13.patch"
        "rust-1.56.patch"
        "mozconfig"
        "xulrunner-27.0-build-arm.patch"
        "bug-1163171.patch")
sha256sums=('756fc69478b9913c1f3c91090b78d880da8f593238754b8416c87184e4d6e597'
            '431601328f59578194ad57fccd70f4c9277605cda1ec77a7a2c68502a4cd6fa0'
            'a125460818373bc5b4f7eda7598dee71fd3aef3f175e761ec60c356532f9fd45'
            '2b1404af0c6db2602bdf6449a936d04fbb6e97299addd5e21c55b0ceff43d891'
            'bc83b8a51c81a401da2fbe1f7cd0843248c32607d3a51119585df074d797922e'
            '6917d93f905af5a068e2daa598d8f64aacb47c69fc4a9e5263a583857abb7e29')
install="$pkgname.install"

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact foutrelis@archlinux.org for
# more information.
_google_api_key=AIzaSyDwr302FpOSkGRpLlUpPThNTDPbXcIn_FM

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Arch Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact heftig@archlinux.org for
# more information.
_mozilla_api_key=e05d56db0a694edc8b5aaebda3f2db6a

prepare() {
  cd "$pkgname-$pkgver"
  cp ../mozconfig .mozconfig

  patch -Np1 -i ../clang-13.patch
  patch -Np1 -i ../rust-1.56.patch

  echo -n "$_google_api_key" > google-api-key
  echo -n "$_mozilla_api_key" > mozilla-api-key

  cat >> .mozconfig <<EOF
  ac_add_options --with-google-location-service-api-keyfile=${PWD@Q}/google-api-key
  ac_add_options --with-google-safebrowsing-api-keyfile=${PWD@Q}/google-api-key
  ac_add_options --with-mozilla-api-keyfile=${PWD@Q}/mozilla-api-key

  # ALARM
  ac_add_options --disable-webrtc
EOF

  [[ $CARCH != "aarch64" ]] && echo "ac_add_options --disable-elf-hack" >> .mozconfig
  LDFLAGS+=" -Wl,--no-keep-memory -Wl,--reduce-memory-overheads"
  patch -p2 -i "${srcdir}"/xulrunner-27.0-build-arm.patch
  patch -p1 -i "${srcdir}"/bug-1163171.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./mach build
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="$pkgdir" INSTALL_SDK= ./mach install

  rm -rf "$pkgdir"/usr/lib/seamonkey/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  install -Dm644 comm/suite/branding/seamonkey/default128.png \
          "$pkgdir/usr/share/pixmaps/seamonkey.png"

  install -Dm644 toolkit/mozapps/installer/linux/rpm/mozilla.desktop \
                "$pkgdir/usr/share/applications/seamonkey.desktop"
  sed -i 's/@MOZ_APP_DISPLAYNAME@/SeaMonkey internet suite/' \
                "$pkgdir/usr/share/applications/seamonkey.desktop"
  sed -i 's/@MOZ_APP_NAME@/seamonkey/' "$pkgdir/usr/share/applications/seamonkey.desktop"
}
