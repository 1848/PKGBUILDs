# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>
# Contributor: Jack Lloyd <jack@randombit.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - compiler flags adjustment to build correctly
#  - --cpu in configure set to correct architectures, autodetect fails
#  - AArch64 patch

pkgname=botan
pkgver=2.5.0
pkgrel=4
pkgdesc='Crypto library written in C++'
arch=('x86_64')
url='https://botan.randombit.net/'
license=('BSD')
makedepends=('git' 'python')
# Using a specific commit instead of the .tgz because botan issue #1524
source=('git+https://github.com/randombit/botan#commit=6c5d9ef3c49658cbf4096e04905e3c0debb9d096'
        '0001-aarch64-support.patch')
md5sums=('SKIP'
         '37165d2a365dd2e9d779b89fa55ba77b')
#validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')
#source=("https://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
#sha256sums=('b8a31fe03e7f048a5bd3967ecd04b6a48966215e78792df06e333b0eede4fb1b'
#            'SKIP')

prepare() {
  cd "$pkgname"
  #cd "${pkgname^}-$pkgver"

  patch -p1 -i ../0001-aarch64-support.patch
}

build() {
  cd "$pkgname"
  #cd "${pkgname^}-$pkgver"

  if [[ $CARCH == 'aarch64' ]]; then
    CPU='aarch64'
  elif [[ $CARCH == 'armv7h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfpv3-d16 -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv7-a'
  elif [[ $CARCH == 'armv6h' ]]; then
    sed -i 's/lang_flags "/lang_flags "-mfpu=vfp -mfloat-abi=hard /' src/build-data/cc/gcc.txt
    CPU='arm/armv6'
  elif [[ $CARCH == 'arm' ]]; then
    CPU='arm/armv5te'
  fi

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon"

  # botan benefits from -O3, and the developers runs tests with it
  CXXFLAGS="$CXXFLAGS -O3" ./configure.py --prefix=/usr --with-bzip --with-zlib --with-lzma --cpu=$CPU $CONFIG
  make
}

check() {
  cd "$pkgname"

  ./botan-test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim: ts=2 sw=2 et:
