# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - strip -mavx from configure and Makefile

pkgname=libsemigroups
pkgver=1.0.4
pkgrel=2
pkgdesc="C++ library for semigroups and monoids"
url="https://libsemigroups.github.io/libsemigroups/"
arch=(x86_64)
license=(GPL3)
depends=(gcc-libs)
makedepends=(fmt)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4956c868c41b3421da211f413b4b104b7882640ebb464314e8b12f7b6e10fcb0')

build() {
  cd $pkgname-$pkgver
  sed -i 's/-mavx//g' configure.ac Makefile.am
  autoreconf -fi
  ./configure --prefix=/usr --with-external-fmt
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -e 's|lsemigroups|lsemigroups-1.0|g' -i "$pkgdir"/usr/lib/pkgconfig/libsemigroups.pc # Fix lib name in pkgconfig
}
