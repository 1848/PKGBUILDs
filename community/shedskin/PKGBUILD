# $Id: PKGBUILD 82726 2013-01-23 15:21:35Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix flags for ARM architectures
#  - change from being an 'any' package to architecture-specific

pkgname=shedskin
pkgver=0.9.3
pkgrel=1
pkgdesc='Python to C++ compiler'
arch=('arm' 'armv6h' 'armv7h')
url='http://code.google.com/p/shedskin/'
license=('GPL2')
depends=('pcre' 'gc' 'python2')
conflicts=('shedskin-svn')
source=("http://shedskin.googlecode.com/files/$pkgname-$pkgver.tgz"
        "$pkgname.py")
sha256sums=('cd81f5af25a5937ecca139ab2877d80fbc29020d34c97e65d0590b993f11918d'
            '26527327dba7d6a34c37a931bfce925d7802535626a92f35a2890d698325c712')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ $CARCH == "arm" ]] && sed -i 's/-march=native/-march=armv5te/' shedskin/FLAGS
  [[ $CARCH == "armv6h" ]] && sed -i 's/-march=native/-march=armv6 -mfloat-abi=hard -mfpu=vfp/' shedskin/FLAGS
  [[ $CARCH == "armv7h" ]] && sed -i 's/-march=native/-march=armv7-a -mfpu=vfpv3-d16 -mfloat-abi=hard/' shedskin/FLAGS

  install -d "$pkgdir/usr/bin"
  install -Dm755 "../$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  python2 setup.py install --prefix="$pkgdir/usr"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
