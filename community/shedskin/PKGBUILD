# $Id: PKGBUILD 82726 2013-01-23 15:21:35Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix flags for ARM architectures
#  - change from being an 'any' package to architecture-specific

pkgname=shedskin
pkgver=0.9.4
pkgrel=1
pkgdesc='Python to C++ compiler'
arch=('arm' 'armv6h' 'armv7h')
url='http://code.google.com/p/shedskin/'
license=('GPL2')
depends=('pcre' 'gc' 'python2')
source=("http://shedskin.googlecode.com/files/$pkgname-$pkgver.tgz")
sha256sums=('81fc79660b94c5c566ffb07ccc42db2ace99f23fd867e00ccf48bdc0dfcffc5b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ $CARCH == "arm" ]] && sed -i 's/-march=native/-march=armv5te/' shedskin/FLAGS
  [[ $CARCH == "armv6h" ]] && sed -i 's/-march=native/-march=armv6 -mfloat-abi=hard -mfpu=vfp/' shedskin/FLAGS
  [[ $CARCH == "armv7h" ]] && sed -i 's/-march=native/-march=armv7-a -mfpu=vfpv3-d16 -mfloat-abi=hard/' shedskin/FLAGS

  sed -i 's:/bin/python2:/usr/bin/python2:' "scripts/$pkgname"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix="$pkgdir/usr"
}

# vim:set ts=2 sw=2 et:
