# Maintainer: Antonio Rojas <arojas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - explicitly link v5/v6 with libatomic

pkgname=fplll
pkgver=5.3.1
pkgrel=1
pkgdesc="Lattice algorithms using floating-point arithmetic"
arch=(x86_64)
url="https://github.com/fplll/fplll"
license=(LGPL)
depends=(mpfr qd)
conflicts=(libfplll)
provides=(libfplll)
replaces=(libfplll)
source=("https://github.com/fplll/fplll/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('bf7e7e667173b5655cb989ec6a55c07af057d9011572f85eb53fbf93f4e2d239')

build() {
  cd $pkgname-$pkgver
  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && LIBS="-latomic"
  LIBS=$LIBS ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
