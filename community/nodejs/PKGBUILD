# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - set config and gyp flags
#  - patch to link libatomic for v6

buildarch=28

pkgname=nodejs
pkgver=13.5.0
pkgrel=2
pkgdesc='Evented I/O for V8 javascript'
arch=('x86_64')
url='https://nodejs.org/'
license=('MIT')
depends=('openssl' 'zlib' 'icu' 'libuv' 'c-ares' 'libnghttp2') # 'http-parser' 'v8')
makedepends=('python' 'procps-ng')
optdepends=('npm: nodejs package manager')
source=("nodejs-$pkgver.tar.gz::https://github.com/nodejs/node/archive/v$pkgver.tar.gz"
        'atomic.patch')
sha512sums=('d2b8ffa7b83ed1266b3d7c95efbe42166e6b26f8153a19c54abf27f4156e42b9ee20117b689c9df53e6a4e583d788702918914063031e05156c767a38e8ec46e'
            '3d7378b80a9729f5dd5c6c7446531244a67bd6021ccf6feb270af2b698d9bd2ff3c6bf84f8d0a6eb3671c59ea920de8ec081cac0b705236b474b61427f5c1e39')

prepare() {
  cd node-$pkgver

  if [[ $CARCH == "armv6h" ]]; then
    patch -p0 -i ../atomic.patch
  fi
}

build() {
  cd node-$pkgver

  [[ "$CARCH" == "armv6h" ]] && export CONFIG="--with-arm-float-abi=hard --with-arm-fpu=vfp" && export GYPFLAGS="-Darm_thumb -Darm_float_abi=hard -Darm_version=6 -Darm_fpu=vfpv2"
  [[ "$CARCH" == "armv7h" ]] && export CONFIG="--with-arm-float-abi=hard --with-arm-fpu=vfpv3-d16" && export GYPFLAGS="-Darm_thumb -Darm_float_abi=hard -Darm_version=7 -Darm_fpu=vfpv3-d16"

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-cares \
    --shared-nghttp2 \
    ${CONFIG}
    # --shared-v8
    # --shared-http-parser

  make
}

check() {
  cd node-$pkgver
  # Expected failure: https://github.com/nodejs/node/issues/11627
  make test || warning "Tests failed"
}

package() {
  cd node-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE \
    "$pkgdir"/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
