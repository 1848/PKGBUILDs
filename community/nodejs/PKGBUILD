# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>
# Contributor: Jason Plum <max@warheads.net>

pkgname=nodejs
pkgver=0.8.3
pkgrel=2
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64' 'arm')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'v8')
checkdepends=('curl') # curl used for check()
optdepends=('openssl: TLS support')
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz")
md5sums=('53604d2e3773d62e90fa5a340a362e08')

build() {
  cd node-v${pkgver}

  # fs#30766
  sed -i "s|var npmDir = path.resolve(node_prefix, 'lib/node_modules/npm');|var npmDir = path.join(dest_dir, node_prefix, 'lib/node_modules/npm');|" tools/installer.js

  msg 'fixing for python2 name'
  find -type f -exec sed -e 's_^#!/usr/bin/env python$_&2_' -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' -e 's_^#!/usr/bin/python$_&2_' -e "s_'python'_'python2'_" -i {} \;
  find test -type f -exec sed -e "s|python |python2 |" -i {} \;
  sed -i "s|python |python2 |" Makefile

  export PYTHON=python2
# we're going to attept to make use of this example
# https://github.com/joyent/node/issues/2131#issuecomment-3208846
# https://gist.github.com/1574158   

  ./configure \
    --prefix=/usr \
    --shared-openssl \
    --shared-v8 \
    --shared-v8-libpath=/usr/lib \
    --shared-v8-includes=/usr/include \
    --without-snapshot

  make
}

check() {
  cd node-v${pkgver}

  make test || true
}

package() {
  cd node-v${pkgver}

  make DESTDIR=${pkgdir} install

  # install docs as per user request
  install -d ${pkgdir}/usr/share/doc/nodejs
  cp -r doc/api/* \
    ${pkgdir}/usr/share/doc/nodejs

  # compress man pages
  find $pkgdir/usr/lib/node_modules/npm/man -type f | xargs gzip -9

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
