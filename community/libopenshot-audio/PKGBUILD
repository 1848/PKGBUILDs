# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Foster McLane <fkmclane@gmail.com>
# Contributor: Jonathan Thomas <jonathan@openshot.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link v5/v6 with libatomic

pkgname=libopenshot-audio
pkgver=0.1.8
pkgrel=2
pkgdesc="A high-quality audio editing and playback library used by libopenshot."
arch=('x86_64')
url="https://openshot.org/"
license=('GPL3')
depends=('alsa-lib' 'freetype2' 'libxcursor' 'libxinerama' 'libxrandr')
makedepends=('cmake' 'doxygen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenShot/libopenshot-audio/archive/v$pkgver.tar.gz"
	"$pkgname-pr-39.patch::https://patch-diff.githubusercontent.com/raw/OpenShot/libopenshot-audio/pull/39.patch"
	'atomic.patch')
sha256sums=('384d0ef39c78f16d77048de3c96152321724084f978dc622675dd6bb16e15e19'
            'ae784a96730f7cf09ba7bf29d06704ba6a0eca662d242d786b0a86a4343e9dc9'
            'f066a57428a6239af4cde7a81a6dc579cc85062f4877def11b0650673f34eb6f')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i "$srcdir/$pkgname-pr-39.patch"
	if [[ $CARCH == "armv6h" || $CARCH == "arm" ]]; then
		patch -p0 -i ../atomic.patch
	fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cd build
	make DESTDIR="${pkgdir}" install
}
