# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - ARM patch, use NO_CRASH_HANDLER=1 for AArch64

pkgname=texstudio
pkgver=2.12.0
pkgrel=2
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11')
makedepends=('qt5-tools' 'mercurial')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("hg+http://hg.code.sf.net/p/texstudio/hg#tag=${pkgver}"
        "fs52529.patch"
        texstudio-debughelper-arm-fix.patch)
sha512sums=('SKIP'
            '562f49ddab6e39ac0a05dd67a2eb14e6ce47af44cfbc7b850edb6e4189b8c2ddebb6c4c118b4e8faeb0b49c3180d8cc24f5eb45b421f6acac996fd332a58f8d7'
            'dcb5ce7cd3e83fc221903fa895e6c428c54ada7468e74bb0a3e6bfac01c9e9575e4780d300b67f44064f2d25e10dd1aef531b847cdee71fbfbe7337808a0c8f4')

prepare() {
    cd hg
    patch -Np1 < "$srcdir"/fs52529.patch
    patch -p1 -i ../texstudio-debughelper-arm-fix.patch
}

build() {
    cd hg
    [[ $CARCH == "aarch64" ]] && EXTRA="NO_CRASH_HANDLER=1"
    qmake-qt5 CONFIG-="debug" $EXTRA texstudio.pro
    make
}

package() {
    cd hg
    make INSTALL_ROOT="$pkgdir" install
}
