# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix build on v5

pkgname=rubberband
pkgver=1.9
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('x86_64')
url="https://www.breakfastquay.com/rubberband/"
license=('GPL2')
depends=('gcc-libs' 'glibc')
makedepends=('fftw' 'ladspa' 'java-environment' 'libsamplerate' 'libsndfile'
'vamp-plugin-sdk')
provides=('librubberband.so' 'librubberband-jni.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/rubberband/archive/v$pkgver.tar.gz"
        '0001-fix-armv5-build.patch')
sha512sums=('4899f1def498e8e8dabf5d690d9716cbec9f6b7836776496fef2089dab75f52470128325b3b653a3136e5b226a31120869f028c8c9af9afac381700c7c57ccb5'
            '2b9935a702c1198eae93e00b0c88429737064b0d3a252f314044eef02e1c8a09ff9c98124be6339b502a5821412e6a45f67860290a88f7295c2d05c060efcb7c')
b2sums=('88fc7c8b91e9ef7e78c6a00b08f165f3c8a14feefc82f972fdfe438272b64bd458840bc1a384b0f5da431085251e6e423404d26ce701900f83faeaa47b986b16'
        '4a28b09d40a60b7bddd939690999137a864fafe1aaef315bf0ef89c69d85751ebda671ad8f132e2e9e7b518180c8c0751288bc7cb07f3c7496620a33a1509b91')

prepare() {
  cd "$pkgname-$pkgver"
  [[ $CARCH == 'arm' ]] && patch -p1 -i ../0001-fix-armv5-build.patch
  autoreconf -vfi
}

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  export JAVA_HOME="/usr/lib/jvm/java-${java_major}-openjdk"
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
  make jni
}

package() {
  depends+=('libfftw3.so' 'libsamplerate.so' 'libsndfile.so' 'libvamp-sdk.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
