# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

# ALARM: jdoe0
# --enable-aligned flag is required to work correctly on armv5

buildarch=1

pkgname=libtorrent
pkgver=0.13.3
pkgrel=2.1
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='http://libtorrent.rakshasa.no/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl')
source=("http://libtorrent.rakshasa.no/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('e65a20b9e6d5c04b4e0849543d58befb60d948b7')

options=('!libtool')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"

	if [[ $CARCH == 'arm' ]]; then
		./configure \
			--prefix=/usr \
			--enable-aligned \
			--disable-debug
	else
		./configure \
			--prefix=/usr \
			--disable-debug
	fi

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
