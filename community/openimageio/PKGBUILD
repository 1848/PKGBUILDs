# $Id: PKGBUILD 90071 2013-05-06 19:38:49Z foutrelis $
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed intel-tbb from deps, set -DUSE_TBB=0
#  - patch to block out x86 asm (https://github.com/OpenImageIO/oiio/pull/546)

pkgname=openimageio
pkgver=1.1.10
pkgrel=2
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew' 'libtiff' 'opencolorio')
makedepends=('cmake' 'qt4' 'python2' 'boost' 'mesa')
optdepends=('qt4: iv image viewer'
            'python2: bindings support')
source=(https://github.com/OpenImageIO/oiio/tarball/Release-$pkgver
        arm.patch)
md5sums=('e5c5cd77238f74c3a6a3610c211430f2'
         '7cf2d71031c5f970edf481f55a594ecc')

build() {
  cd "$srcdir"/$_pkgname*

  patch -p1 -i "$srcdir"/arm.patch

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
      -DUSE_EXTERNAL_TBB=ON \
      -DUSE_TBB=0 \
      ..
  make
}

package() {
  cd "$srcdir"/$_pkgname*/src/build

  make DESTDIR="$pkgdir" install

  # license
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
