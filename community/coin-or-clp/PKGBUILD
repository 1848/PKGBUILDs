# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to remove MMX headers

pkgname=coin-or-clp
pkgver=1.17.0
pkgrel=1
pkgdesc="COIN-OR linear programming solver"
arch=(x86_64)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("https://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz"
        0001-Remove-MMX-header.patch)
sha256sums=('e92cda578075289a2b70c90f2461c985523c5331c65c2a4f16abfc6da2a77adc'
            'c7b7998438cc250bae33934d9f7ce0fe4600b39a71faf88f00542b938084ebcd')

prepare() {
  cd Clp-$pkgver/Clp
  patch -p2 -i $srcdir/0001-Remove-MMX-header.patch
}

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
