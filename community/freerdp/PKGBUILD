# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - define WITH_NEON and ARM_FP_ABI to fix retard developer assumptions

pkgname=freerdp
pkgver=1.2.0_beta1+android9
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/+/-}
pkgrel=2
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse' 'libxkbfile'
	 'libxrender' 'libxfixes' 'gst-plugins-base-libs')
makedepends=('krb5' 'cmake' 'damageproto' 'fixesproto' 'renderproto'
	     'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/${pkgver/_/-}.tar.gz
	build-fix.patch)
md5sums=('a4a0a4f6e69c871aeab0600091ca6118'
         '9e9488538fe5ab74c25e2fb69e3edf62')

prepare() {
  cd $srcdir/FreeRDP-${_pkgver}
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd $srcdir/FreeRDP-${_pkgver}

  [[ $CARCH == "arm" ]] && ABI="soft"
  [[ $CARCH == "armv6h" || $CARCH == "armv7h" ]] && ABI="hard"

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	-DWITH_CUPS=ON -DCHANNEL_PRINTER=ON \
	-DWITH_NEON=OFF \
	-DARM_FP_ABI=${ABI} \
	.
  make
}

package() {
  cd $srcdir/FreeRDP-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
