# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patches to fix ARM build

_hkgname=foundation
pkgname=haskell-foundation
pkgver=0.0.2
pkgrel=1
pkgdesc="Alternative prelude with batteries and no dependencies"
url="https://github.com/haskell-foundation/foundation"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=8.0.1')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        'https://github.com/haskell-foundation/foundation/commit/622c6338a6315a2301337f725dc8391757849596.patch'
        'https://github.com/haskell-foundation/foundation/commit/1154e23b4be53acf0328ae730c3b07c4b0b9a665.patch')
md5sums=('98fdbf309ca08e2a5f87842531ac8ca1'
         '6e65821afc9c291ead01830aaf568346'
         'e9b0e5b8ec29690d1a16f740bebba37b')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    patch -p1 -i ../622c6338a6315a2301337f725dc8391757849596.patch
    patch -p1 -i ../1154e23b4be53acf0328ae730c3b07c4b0b9a665.patch
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
