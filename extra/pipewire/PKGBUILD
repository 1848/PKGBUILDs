# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on valgrind

pkgname=pipewire
pkgver=0.2.6
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="https://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base sbc ffmpeg rtkit)
makedepends=(git meson doxygen graphviz xmltoman)
install=pipewire.install
_commit=4aff470a8fa1e06448b2316781b4e7a91fbda341  # tags/0.2.6
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -D docs=true \
    -D man=true
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
