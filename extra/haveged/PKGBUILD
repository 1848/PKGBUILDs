# $Id$
# Maintainer:  Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch from Alpine Linux to fix cache detection issues on ARM

pkgname=haveged
pkgver=1.9.4
pkgrel=2
pkgdesc='Entropy harvesting daemon using CPU timings'
arch=('x86_64')
url='https://github.com/jirka-h/haveged'
license=('GPL')
depends=('glibc')
validpgpkeys=('8F6B7293B03E5AC5762B65CE2E2F252B05CCA168') # Jirka Hladky <hladky.jiri@gmail.com>
source=("$pkgname-$pkgver.tar.gz::https://github.com/jirka-h/$pkgname/archive/$pkgver.tar.gz"
        "https://github.com/jirka-h/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.asc"
        'haveged.service'
        'fix-cpu-cache-size-detection.patch')
sha256sums=('c4959d3cb1fa6391d16a3aa1ba4d82cd3a0d497206ae4b87d638088c0664e5aa'
            'SKIP'
            '5b9b5d90c70a60291f71f18e337c6822e8850945bddeef4d061acb0305bccf25'
            '5aecfd72a1dbb9cc9b32d5677917d8a59dba7a2e021e31dee95f3f5b9e7ecf28')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../fix-cpu-cache-size-detection.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
      --prefix=/usr \
      --bindir=/usr/bin \
      --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m0644 "${srcdir}"/haveged.service "$pkgdir"/usr/lib/systemd/system/haveged.service
}
