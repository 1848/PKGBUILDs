# $Id: PKGBUILD 162354 2012-06-25 18:50:36Z foutrelis $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to build on ARM

pkgbase=calligra
pkgname=('calligra-filters'
         'calligra-interfaces'
         'calligra-libs'
         'calligra-plugins'
         'calligra-pics'
         'calligra-servicetypes'
         'calligra-templates'
         'calligra-tools'
         'calligra-karbon'
         'calligra-kexi'
         'calligra-kounavail'
         'calligra-plan'
         'calligra-stage'
         'calligra-krita'
         'calligra-sheets'
         'calligra-words'
         'calligra-handbook'
         'calligra-braindump'
         'calligra-flow')
pkgver=2.4.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('cmake' 'automoc4' 'boost ' 'eigen2'
             'kdepimlibs' 'libpqxx' 'freetds' 'xbase'
             'kdegraphics-okular' 'gsl' 'kdeedu-marble'
             'libwpd' 'libwpg' 'pstoedit' 'glew'
             'fftw' 'opengtl' 'lcms2' 'libkdcraw' 'wv2')
groups=('calligra')
source=("http://download.kde.org/stable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2"
        "compile_on_arm.diff")
md5sums=('469d639f17e3d6a186baabd899147e39'
         'c8febb3462af3a19c533fc2754e75947')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -p1 -i "${srcdir}/compile_on_arm.diff"
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_active=OFF \
    -DBUILD_mobile=OFF \
    -DIHAVEPATCHEDQT=1
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libwpg: Corel WordPerfect Graphics image importer')
  conflicts=('koffice-filters')
  replaces=('koffice-filters')
  install=calligra-filters.install

  cd "${srcdir}"/build/filters
  make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces() {
  pkgdesc="Interfaces for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-interfaces')
  replaces=('koffice-interfaces')

  cd "${srcdir}"/build/interfaces
  make DESTDIR="${pkgdir}" install
}

package_calligra-libs() {
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms2' 'calligra-servicetypes')
  conflicts=('koffice-libs')
  replaces=('koffice-libs')

  cd "${srcdir}"/build/libs
  make DESTDIR="${pkgdir}" install
}

package_calligra-plugins() {
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs' 'calligra-filters')
  optdepends=('kdeedu-marble: map shape for Calligra')
  conflicts=('koffice-plugins' 'koffice-kchart')
  replaces=('koffice-plugins' 'koffice-kchart' 'koffice-kformula')

  cd "${srcdir}"/build/plugins
  make DESTDIR="${pkgdir}" install
}

package_calligra-pics() {
  pkgdesc="Icons for the Calligra office suite"
  conflicts=('koffice-pics')
  replaces=('koffice-pics')
  install=calligra.install

  cd "${srcdir}"/build/pics
  make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes() {
  pkgdesc="Servicetypes for the Calligra office suite"
  conflicts=('koffice-servicetypes')
  replaces=('koffice-servicetypes')
  install=calligra.install

  cd "${srcdir}"/build/servicetypes
  make DESTDIR="${pkgdir}" install
}

package_calligra-templates() {
  pkgdesc="Templates for the Calligra office suite"
  conflicts=('koffice-templates')
  replaces=('koffice-templates')

  cd "${srcdir}"/build/templates
  make DESTDIR="${pkgdir}" install
}

package_calligra-tools() {
  pkgdesc="Tools for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-tools')
  replaces=('koffice-tools')
  install=calligra-tools.install

  cd "${srcdir}"/build/tools
  make DESTDIR="${pkgdir}" install
}

package_calligra-karbon() {
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'calligra-templates' 'kdebase-runtime')
  optdepends=('calligra-filters: import/export filters'
              'calligra-tools: extra calligra tools')
  conflicts=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc')
  replaces=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc')
  install=calligra-karbon.install

  cd "${srcdir}"/build/karbon
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi() {
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs' 'calligra-templates')
  optdepends=('libmysqlclient: MySQL driver'
              'libpqxx: PostgreSQL driver'
              'freetds: Sybase & MS SQL driver'
              'xbase: XBase driver'
              'calligra-sheets: Spreadsheet-to-Kexi-table import plugin'
              'kdeedu-marble: map form widget'
              'calligra-plugins: extra calligra plugins')
  conflicts=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc')
  replaces=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc')
  install=calligra-kexi.install

  cd "${srcdir}"/build/kexi
  make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail() {
  pkgdesc="Unavailable Calligra Document"
  depends=('calligra-libs')
  conflicts=('koffice-kounavail')
  replaces=('koffice-kounavail')

  cd "${srcdir}"/build/kounavail
  make DESTDIR="${pkgdir}" install
}

package_calligra-plan() {
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words')
  conflicts=('koffice-kplato' 'calligra-kplato')
  replaces=('koffice-kplato' 'calligra-kplato')
  install=calligra-plan.install

  cd "${srcdir}"/build/plan
  make DESTDIR="${pkgdir}" install
}

package_calligra-stage() {
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'calligra-templates' 'kdebase-runtime')
  optdepends=('calligra-plugins: extra calligra plugins')
  conflicts=('koffice-kpresenter' 'koffice-kpresenter-doc' 'calligra-stage-doc')
  replaces=('koffice-kpresenter' 'koffice-kpresenter-doc' 'calligra-stage-doc')
  install=calligra-stage.install

  cd "${srcdir}"/build/stage
  make DESTDIR="${pkgdir}" install
}

package_calligra-krita() {
  pkgdesc="Edit and paint images"
  depends=('calligra-templates' 'calligra-plugins' 'kdebase-runtime'
           'qimageblitz' 'poppler-qt' 'opengtl' 'glew' 'libkdcraw' 'fftw')
  conflicts=('koffice-krita' 'calligra-krita-doc')
  replaces=('koffice-krita' 'calligra-krita-doc')
  install=calligra-krita.install

  cd "${srcdir}"/build/krita
  make DESTDIR="${pkgdir}" install
}

package_calligra-sheets() {
  pkgdesc="Write spreadsheet documents"
  depends=('calligra-templates' 'calligra-libs' 'gsl' 'kdebase-runtime')
  optdepends=('calligra-filters: import/export filters'
              'calligra-plugins: extra calligra plugins'
              'calligra-tools: extra calligra tools')
  conflicts=('koffice-kspread' 'koffice-kspread-doc' 'calligra-tables-doc' 'calligra-tables')
  replaces=('koffice-kspread' 'koffice-kspread-doc' 'calligra-tables-doc' 'calligra-tables')
  install=calligra-sheets.install

  cd "${srcdir}"/build/sheets
  make DESTDIR="${pkgdir}" install
}

package_calligra-words() {
  pkgdesc="Word Processor"
  depends=('calligra-templates' 'calligra-pics' 'calligra-kounavail'
           'calligra-plugins' 'kdegraphics-okular')
  optdepends=('calligra-tools: extra calligra tools')
  conflicts=('koffice-kword')
  replaces=('koffice-kword')
  install=calligra-words.install

  cd "${srcdir}"/build/words
  make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook')
  replaces=('koffice-handbook')

  cd "${srcdir}"/build/doc/calligra
  make DESTDIR="${pkgdir}" install
}

package_calligra-braindump() {
  pkgdesc="Notes and idea gathering"
  depends=('calligra-libs' 'kdebase-runtime')
  install=calligra-braindump.install

  cd "${srcdir}"/build/braindump
  make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
  pkgdesc="Flowchart & Diagram Editing"
  depends=('calligra-words' 'kdebase-runtime')
  conflicts=('calligra-flow-doc')
  replaces=('calligra-flow-doc')
  install=calligra-flow.install

  cd "${srcdir}"/build/flow
  make DESTDIR="${pkgdir}" install
}
