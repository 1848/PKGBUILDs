# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream XMP patch to fix arm FTBFS

pkgname=exempi
pkgver=2.6.1
pkgrel=1
pkgdesc="A library to parse XMP metadata"
url="https://libopenraw.freedesktop.org/wiki/Exempi"
arch=(x86_64)
license=(custom:BSD)
depends=(gcc-libs expat zlib)
makedepends=(boost)
source=(https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2{,.asc}
        https://patch-diff.githubusercontent.com/raw/adobe/XMP-Toolkit-SDK/pull/57.patch)
sha256sums=('072451ac1e0dc97ed69a2e5bfc235fd94fe093d837f65584d0e3581af5db18cd'
            'SKIP'
            '284d04cf4c158018dddc5e46b48833dbffdf0407f834f6417ebeb4ff62e81b54')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../57.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr # --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
