# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to link with libatomic on v5/v6
#  - disable jit on v5/v6
#  - use tarball source

highmem=1

pkgname=qt5-webkit
_commit=95401fe3908e0b4eebc9447e678298d768617bc7
pkgver=qtwebkit+5.212.0alpha2+156+g95401fe3908
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
depends=(qt5-location qt5-sensors qt5-webchannel libwebp libxslt libxcomposite gst-plugins-base hyphen)
makedepends=(cmake ruby gperf python2 qt5-doc qt5-tools git)
optdepends=('gst-plugins-good: Webm codec support')
conflicts=(qt5-webkit-ng qt5-webkit-og)
replaces=(qt5-webkit-ng qt5-webkit-og)
_pkgfqn="${pkgname/5-/}-opensource-src-${_qtver}"
source=(https://github.com/annulen/webkit/archive/$_commit.tar.gz
        atomic.patch)
sha256sums=('abb506f757a195fb92f9ebdc6b4119c3961a1a40b44b1c1b6f8dceab5a2c70d2'
            '23fbd785daf116c71b3ce87d40860f0a6c56ab1065ee98ec41634626ca961129')

#pkgver() {
#  cd webkit
#  git describe --tags | sed -e 's/-/+/g' -e 's/+alpha/alpha/' -e 's/v//'
#}

prepare() {
  mkdir -p build

  if [[ $CARCH == "arm" || $CARCH == "armv6h" ]]; then
    cd webkit-$_commit
    patch -p0 -i ../atomic.patch
  fi
}

build() {
  cd build

  [[ $CARCH == "arm" || $CARCH == "armv6h" ]] && CONFIG="-DENABLE_JIT=OFF"

  cmake ../webkit-$_commit \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DPORT=Qt \
    -DENABLE_TOOLS=OFF \
    $CONFIG
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}

# Fix pkgconfig files
  sed -e 's|qt/Qt5WebKit|qt/QtWebKit|' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKit.pc
  sed -e 's|qt/Qt5WebKitWidgets|qt/QtWebKitWidgets|' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKitWidgets.pc
  sed -e '/Name/a Description: Qt WebKit module' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKit.pc
  sed -e '/Name/a Description: Qt WebKitWidgets module' -i "$pkgdir"/usr/lib/pkgconfig/Qt5WebKitWidgets.pc
}
