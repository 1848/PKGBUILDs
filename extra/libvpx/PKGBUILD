# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building
#  - disable neon and neon-asm

pkgname=libvpx
pkgver=1.9.0
pkgrel=1
pkgdesc='VP8 and VP9 codec'
arch=(x86_64)
url=http://www.webmproject.org/
license=(BSD)
depends=(gcc-libs)
makedepends=(
  git
  nasm
)
provides=(libvpx.so)
_tag=6516e974f8c40d0e49b19a4b55b1c98e7432edbb
source=(git+https://chromium.googlesource.com/webm/libvpx#tag=${_tag}
        0001-ARM-fixes.patch)
sha256sums=('SKIP'
            '65504659ca0fdc10b3ad8a82ad902aa97fd7a31f31377a217ca9538722961f22')

pkgver() {
  cd libvpx

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd libvpx

  patch -p1 -i ../0001-ARM-fixes.patch
}

build() {
  cd libvpx

  [[ $CARCH != "aarch64" ]] && CONFIG="--disable-neon --disable-neon-asm"
  ./configure \
    --prefix=/usr \
    --disable-install-docs \
    --disable-install-srcs \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-vp9-temporal-denoising $CONFIG
  make
}

package() {
  cd libvpx

  make DIST_DIR="${pkgdir}"/usr install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/libvpx/
}

# vim: ts=2 sw=2 et:
