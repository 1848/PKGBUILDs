# Maintainer: Daniel Isenmann <daniel@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cherry-pick upstream patch to fix FTBFS

pkgname=gegl
pkgver=0.4.18
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="https://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'luajit' 'jasper' 'lensfun' 'suitesparse')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'gobject-introspection' 'git' 'meson' 'python-gobject')
optdepends=('ffmpeg: ffmpeg plugin')
_commit=2556eb7807dd9b3cb97f2769d8993f7a0c38e2a9  # tags/GEGL_0_4_18^0
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd ${pkgname}
  git cherry-pick -n c1c3541caab485384087bbbcf1c83ef2d5bd4d43
  git cherry-pick -n 6bcf95fd0f32cf5e8b1ddbe17b14d9ad049bded8
}

build() {
  mkdir -p build
  cd build
  arch-meson ../$pkgname \
    -Dworkshop=true \
    -Dmrg=disabled
  ninja
}

check() {
  cd build
  ninja test || :
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
