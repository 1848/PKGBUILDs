# $Id: PKGBUILD 157142 2012-04-24 22:33:15Z ibiru $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - use --disable-jit for armv5 to fix segfault
#  - remove noautobuild on next bump

plugrel=1
noautobuild=1

pkgbase=libwebkit
pkgname=(libwebkit libwebkit3)
pkgver=1.8.1
pkgrel=1.1
pkgdesc="An opensource web content engine"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite' 'icu' 'gstreamer0.10-base' 'libsoup' 'enchant' 'libgl'
         'geoclue')
makedepends=('gperf' 'gobject-introspection' 'python2' 'gtk2' 'gtk3' 'mesa')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkit-$pkgver.tar.xz)
sha256sums=('9a126a76b40ca61adb1f1626e1567f976740af2332948cc5189489dbd01e3b28')

build() {
  cd "$srcdir/webkit-$pkgver"
  mkdir build-gtk{2,3}

  # clear makeflags: race during introspection.
  # WebKit*.gir should be created before WebKit*.typelib, but isn't
  MAKEFLAGS=

  [ "${CARCH}" = "arm" ] && CONFIG="--disable-jit"

  ( cd build-gtk2 && _build --with-gtk=2.0 ${CONFIG} )
  ( cd build-gtk3 && _build --with-gtk=3.0 ${CONFIG} )
}

_build() {
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --with-font-backend=freetype \
    --with-unicode-backend=icu \
    --enable-spellcheck "$@"
  make all stamp-po
}

package_libwebkit() {
  pkgdesc+=" (for GTK2)"
  depends+=(gtk2)

  cd "$srcdir/webkit-$pkgver/build-gtk2"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit/LICENSE"
}

package_libwebkit3() {
  pkgdesc+=" (for GTK3)"
  depends+=(gtk3)

  cd "$srcdir/webkit-$pkgver/build-gtk3"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/libwebkit3/LICENSE"
}



