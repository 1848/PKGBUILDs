# $Id: PKGBUILD 149216 2012-02-06 13:29:48Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable arm extensions that aren't supported, autodetection fails

plugrel=1

pkgname=pixman
pkgver=0.24.4
pkgrel=2
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org"
license=('custom')
depends=('glibc')
options=('!libtool')
source=(http://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('683450f917015366ac7918fc517c76801aeff374')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [[ $CARCH = "arm" ]] && CONFIG="--disable-arm-simd --disable-arm-neon --disable-arm-iwmmxt"
  [[ $CARCH = "armv7h" ]] && CONFIG="--disable-arm-neon --disable-arm-iwmmxt"
  ./configure --prefix=/usr --disable-static $CONFIG
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
