# $Id: PKGBUILD 139163 2011-09-30 13:55:54Z schiv $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - build with pulseaudio as sound server, not jack (x86 asm)

plugrel=1

pkgname=snd
pkgver=12.7
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('i686' 'x86_64')
url="http://ccrma.stanford.edu/software/snd/"
license=('custom')
depends=('lesstif' 'pulseaudio' 'gamin' 'gsl' 'fftw')
makedepends=('ladspa' 'wavpack' 'flac' 'speex' 'libxpm'
             'timidity++' 'mpg123' 'vorbis-tools' 'gmp')
optdepends=('ladspa' 'wavpack' 'flac' 'speex'
            'timidity++' 'mpg123' 'vorbis-tools')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('5edb4ee488c3406c9aa582c5f11a4492')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-pulseaudio
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
