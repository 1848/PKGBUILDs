# Maintainer: Laurent Carlier <lordheavym@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - cmake with -DBUILD_CUBE=OFF

pkgname=vulkan-tools
pkgver=1.2.137
pkgrel=1
arch=(x86_64)
url="https://www.khronos.org/vulkan/"
pkgdesc="Vulkan Utilities and Tools"
license=('custom')
depends=(gcc-libs libxcb vulkan-icd-loader)
makedepends=(git cmake python vulkan-headers libx11 wayland)
source=("https://github.com/KhronosGroup/Vulkan-Tools/archive/v${pkgver}.tar.gz"
        0001-cube-Remove-dependency-on-VK_DYNAMIC_STATE_RANGE_SIZ.patch)
sha256sums=('4b9e0aab8e873282a7163b3e9966928188c06697002f8f1ce0c567a06a1b5fda'
            '8da2ac049c5e7d1253e5b55fa9ef0c1f670d17b6a0785561de5edc12e9c9d7a6')

prepare() {
  cd "${srcdir}"/Vulkan-Tools*

  patch -Np1 -i ../0001-cube-Remove-dependency-on-VK_DYNAMIC_STATE_RANGE_SIZ.patch
}

build() {
  cd "${srcdir}"/Vulkan-Tools*

  rm -rf build ; mkdir build ; cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/usr/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_CUBE=OFF \
    -DBUILD_VULKANINFO=ON \
    -DBUILD_ICD=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  groups=('vulkan-devel')
  
  cd "${srcdir}"/Vulkan-Tools*/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
