# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add ARM headers back that were removed upstream

buildarch=28

pkgname=gn
pkgver=0.1568.c599fdbb
_commit=c599fdbbcdb7eb6c7e345e36e1ae5de16a91509f
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=('x86_64')
url="https://gn.googlesource.com/gn/+/master/README.md"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit
        0001-Add-ARM-headers-back.patch)
md5sums=('SKIP'
         '9948af7a864a8e57697c49d15cdcb395')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname
  patch -p1 -i ../0001-Add-ARM-headers-back.patch
}

build() {
  cd $pkgname
  ./build/gen.py
  ninja -C out gn
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
