# $Id: PKGBUILD 136896 2011-09-02 17:27:59Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - removed -malign-double from CFLAGS
#  - removed --enable-sse2 from double precision
#  - removed --enable-sse from single precision

plugrel=1

pkgname=fftw
pkgver=3.3.2
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.fftw.org/"
depends=('glibc' 'bash')
makedepends=('gcc-fortran')
options=('!libtool')
source=("http://www.fftw.org/${pkgname}-${pkgver}.tar.gz")
install=fftw.install
sha1sums=('11a8c31186ff5a7d686a79a3f21b2530888e0dc2')

# notes:
# http://www.fftw.org/fftw2_doc/fftw_6.html#SEC69
# http://www.fftw.org/faq/section2.html#singleprec
# http://www.fftw.org/fftw3_doc/Precision.html#Precision


build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # use upstream default CFLAGS while keeping our -march/-mtune
  CFLAGS+=" -O3 -fomit-frame-pointer -fstrict-aliasing -ffast-math"

  CONFIGURE="./configure F77=gfortran --prefix=/usr \
                 --enable-shared --enable-threads --enable-type-prefix"

  # build & install double precision
  $CONFIGURE
  make
  make DESTDIR=${pkgdir} install
  make clean 

  # build & install long double precission
  $CONFIGURE --enable-long-double
  make
  make DESTDIR=${pkgdir} install
  make clean

  # build & install single precision
  $CONFIGURE --enable-float
  make
  make DESTDIR=${pkgdir} install
}
