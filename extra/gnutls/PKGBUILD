# $Id: PKGBUILD 150689 2012-02-20 10:15:52Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed makedepends on valgrind - v5 doesn't play that game

plugrel=1

pkgname=gnutls
pkgver=3.0.13
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'nettle>=2.4' 'p11-kit>=0.11')
makedepends=('strace')
source=(ftp://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.xz{,.sig}
        buildfix.diff)
md5sums=('ec549be557f31ea8c1738cb441ef23ec'
         'a6a9a1d148abd6377e7634eb03bdaf7c'
         '9b2a32b41391d95aeb8ddae3756fa191')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix hanging make check - http://git.savannah.gnu.org/gitweb/?p=gnutls.git;a=commit;h=642e6b5ca996325dc0ca6401a3b87039408b2585
  patch -Np1 -i ${srcdir}/buildfix.diff
  ./configure --prefix=/usr \
	--with-zlib \
	--disable-static \
	--disable-guile \
	--disable-valgrind-tests
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check # passes all  || /bin/true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
