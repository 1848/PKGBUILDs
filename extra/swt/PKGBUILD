# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - source changes for ARM

pkgname=swt
# http://download.eclipse.org/eclipse/downloads/drops4
pkgver=4.6.2
pkgrel=2
_date=201611241400
pkgdesc='An open source widget toolkit for Java'
arch=('i686' 'x86_64')
url='http://www.eclipse.org/swt/'
license=('EPL')
depends=('java-runtime>=7' 'gtk2>=2.20.1' 'libxtst')
optdepends=('glu' 'webkitgtk2')
makedepends=('java-environment=8' 'libxtst' 'glu' 'unzip' 'pkgconfig' 'webkitgtk2' 'apache-ant')
source=(build-swt.xml)
source_i686=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
source_x86_64=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86_64.zip)
source_arm=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
source_armv6h=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
source_armv7h=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip)
source_aarch64=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86_64.zip)
sha256sums=('3d30fbde25a6a6d260d04cb1782b7618c2a0a926440935a9eea516146eb0e1e1')
sha256sums_i686=('5db81a94b3720e0b7008c52c2f90d8db9b8f8770e2f9ce981c16f284a47f263b')
sha256sums_x86_64=('3315827460e3bbb44812ed7639743ae42886c48c96c44dd06d8d4c319f5f7d50')
sha256sums_arm=('5db81a94b3720e0b7008c52c2f90d8db9b8f8770e2f9ce981c16f284a47f263b')
sha256sums_armv6h=('5db81a94b3720e0b7008c52c2f90d8db9b8f8770e2f9ce981c16f284a47f263b')
sha256sums_armv7h=('5db81a94b3720e0b7008c52c2f90d8db9b8f8770e2f9ce981c16f284a47f263b')
sha256sums_aarch64=('3315827460e3bbb44812ed7639743ae42886c48c96c44dd06d8d4c319f5f7d50')


# These examples don't even run anymore (Try Tux Guitar instead)!
# http://www.eclipse.org/swt/examples.php#standaloneOutsideEclipse
# http://download.eclipse.org/eclipse/downloads/

# http://mirror.ibcp.fr/pub/eclipse/eclipse/downloads/drops4/R-4.4-201406061215/org.eclipse.sdk.examples.source-4.4.zip

build() {
  cd ${srcdir}
  unzip -oq src.zip -d src

  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk

  # Shared objects
  cd src
  ./build.sh

  # SWT jar
  . /etc/profile.d/apache-ant.sh
  ant -f ../build-swt.xml compile
  ant -f ../build-swt.xml jar
}

package() {
  cd ${srcdir}/src

  # Shared objects
  export OUTPUT_DIR=${pkgdir}/usr/lib
  install -dm755 ${OUTPUT_DIR} 
  make -f make_linux.mak install

  # SWT jar
  install -Dm755 ../swt.jar ${pkgdir}/usr/share/java/swt-${pkgver}.jar
  ln -s swt-${pkgver}.jar ${pkgdir}/usr/share/java/swt.jar
}
