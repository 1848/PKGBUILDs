# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed dependency on numactl

pkgname=irqbalance
pkgver=1.5.0
pkgrel=2
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('x86_64')
url="https://github.com/irqbalance/irqbalance"
license=('GPL2')
backup=('etc/irqbalance.env')
depends=('glib2' 'libcap-ng' 'systemd-libs')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('343421fd533d7d161fd05a0edfadaddf1da6ced854a19e9f10ea6c12b97b2ef1629aad855214d9700d31d056bb816bf3ec90cdf7c643a1b6e8af1de5befa64f5')

prepare() {
  cd "$pkgname-$pkgver"
  # fix location of configuration and binary in service
  sed -e 's|/path/to/|/etc/|g' \
      -e 's|/usr/sbin|/usr/bin|g' \
      -i "misc/${pkgname}.service"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd
  make
}

package() {
  cd "$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -vDm 644 misc/irqbalance.service \
    -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 misc/irqbalance.env -t "${pkgdir}/etc/"
  install -vDm 644 {AUTHORS,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
