# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jochen Immend√∂rfer <jochen dot immendoerfer at gmail dot com>

# Kevin Mihelich <kevin@archlinuxarm.org>
#  - import here because arch maintainers can't figure out how to manage files in svn
#  - remove when they extract head from posterior

pkgname=alsa-firmware
pkgver=1.0.27
pkgrel=2
pkgdesc="ALSA firmware package"
arch=('any')
url="http://alsa-project.org/"
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/firmware/$pkgname-$pkgver.tar.bz2)
options=(!strip)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --with-hotplug-dir=/usr/lib/firmware
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # remove files which conflicts with linux-firmware
  rm -rf ${pkgdir}/usr/lib/firmware/{ct{efx,speq}.bin,ess,korg,sb16,yamaha}
  # remove broken symlinks (broken upstream)
  rm -rf ${pkgdir}/usr/lib/firmware/turtlebeach
  # remove empty dir
  rm -rf ${pkgdir}/usr/bin
}
md5sums=('b373b350d5151dd7d64db2fc12936b04')
