# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make -j1

pkgname=libphonenumber
pkgver=7.5.2
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
arch=(i686 x86_64)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest)
url="https://github.com/googlei18n/libphonenumber"
license=("Apache")
source=("$url/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('c116c1299074b10ed8b862221ca57f822bae7637b717706ff2c71350f430b3b1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgname-$pkgver/cpp
  make -j1
}

check() {
  cd build
  make tests
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
