# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sean Middleditch <elanthis@awesomeplay.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - update and patch the fix from 1.79.1-2
#  - remove when upstream adds the patch back in

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=1.1
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=(any)
license=(custom)
url='http://docbook.org/'
depends=(libxml2 libxslt docbook-xml)
install="$pkgname.install"
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F$pkgver/docbook-xsl-$pkgver.tar.gz"
        765567_non-recursive_string_subst.patch)
sha256sums=('966188d7c05fc76eaca115a55893e643dd01a3486f6368733c9ad974fcee7a26'
            '9354a4cd1ae5020d4a378cce7c9ca2162d6835a97155c4a5719927903a0a91f7')

prepare() {
  cd $pkgname-$pkgver

  patch -Np2 -i ../765567_non-recursive_string_subst.patch
}

package() {
  cd $pkgname-$pkgver

  _pkgroot="$pkgdir"/usr/share/xml/docbook/xsl-stylesheets-$pkgver

  install -dm755 ${_pkgroot}
  install -m644 VERSION VERSION.xsl ${_pkgroot}

  for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
      manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
          install -dm755 ${_pkgroot}/${fn}
	  install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
  done

  install -dm755 "$pkgdir"/etc/xml
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
