# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on armv5 and armv6
#  - drop openmpi support

noautobuild=1

pkgname=valgrind
pkgver=3.10.0
pkgrel=1
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.20' 'glibc<2.21' 'perl')
makedepends=('gdb')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        alarm.patch
        valgrind-3.9.0-glibc-2.29.patch)
sha1sums=('aec0b8cd042ec36c8cce4f6027b98627ab202f26'
          '3669a2f6e8c34ad95d05ecc92fe7b2d6661edc76'
          '2080c3d44de20857f18b03a93404a8f3d3721f7e')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.20.patch
  [ "${CARCH}" = "arm" -o "${CARCH}" = "armv6h" ] && patch -Np1 -i ${srcdir}/alarm.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
