# $Id: PKGBUILD 145920 2012-01-04 01:26:04Z allan $
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to fix building on armv5

pkgname=valgrind
pkgver=3.7.0
pkgrel=4
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.16' 'glibc<2.17' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        valgrind-3.7.0-glibc-2.16.patch
        valgrind-3.7.0-supp.patch
        alarm.patch)
md5sums=('a855fda56edf05614f099dca316d1775'
         '8362c9c10b8d7d08d1be628a717cfba7'
         '6cf8d3ace0cdc4c7fd69d61ca0692577'
         '09895dece4b831ceb80285c2175ae937')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -Np1 -i ${srcdir}/valgrind-3.7.0-glibc-2.16.patch
  patch -Np1 -i ${srcdir}/valgrind-3.7.0-supp.patch
  [ "${CARCH}" = "arm" ] && patch -Np1 -i ${srcdir}/alarm.patch

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
