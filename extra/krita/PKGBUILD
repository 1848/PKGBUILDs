# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
pkgver=4.2.2
pkgrel=2
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion karchive kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw curl boost-libs giflib hicolor-icon-theme desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 opencolorio python-pyqt5 libheif sip python-sip)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz"{,.sig}
        krita-global-menu.patch::"https://cgit.kde.org/krita.git/patch/?id=8d6a2a5d")
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../krita-global-menu.patch # Fix global menu on Plasma
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
md5sums=('1100871711c919c956b3e1e2b24e98d4'
         'SKIP'
         'f85e2fe776423589f8398c31787cae9d')
