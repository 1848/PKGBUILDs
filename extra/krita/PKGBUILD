# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
_pkgver=4.4.1
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw boost-libs giflib openjpeg2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 opencolorio python-pyqt5 libheif
             qt5-tools sip5 seexpr-krita)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
            'seexpr-krita: SeExpr generator layer')
source=("https://download.kde.org/stable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz"{,.sig})
sha256sums=('6f2daf068e4e5cef8a738ab47da443dc82ad1463090a4a1544b35201272c3817'
            'SKIP')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F') # Dmitry Kazakov (main key) <dimula73@gmail.com>

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
