# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux,org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - remove makedepend on vc

pkgname=krita
pkgver=4.0.0
pkgrel=2
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kio kitemmodels gsl libraw exiv2 openexr fftw curl boost-libs giflib hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost eigen poppler-qt5 opencolorio python-pyqt5)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/stable/krita/$pkgver/$pkgname-$pkgver.tar.gz"{,.sig}
        krita-l10n.patch::"https://cgit.kde.org/krita.git/patch/?id=3d56e134")
sha256sums=('09c4765e602c7f76e988cef4fe278a43a17933c8fb57c108b4f7f73bf7315593'
            'SKIP'
            'c3a4d07498f77d2d52207748dc162b5693edb22fd567887f3abb477d638aeb0f')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../krita-l10n.patch # Fix translations https://bugs.kde.org/show_bug.cgi?id=391989
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
