# $Id: PKGBUILD 162833 2012-07-01 07:57:51Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enabled OMAP and Exynos APIs
#  - extended no-pthread-stubs.patch for OMAP/Exynos Makefiles

pkgname=libdrm
pkgver=2.4.38
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
replaces=('libdrm-new' 'libdrm-nouveau')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/$pkgname/$pkgname-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING)
sha256sums=('3e5a2f318edaf1eef41e7e6c85aa9596d6f9f8b083ec4d7d6710ffbb8921e7e9'
            'b1f3ae7f2cf80de3451c3f2bc576dbbf8d3a44e5d1f2608f3ebd276c4c32227c'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../no-pthread-stubs.patch

  #libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-vmwgfx-experimental-api \
      --enable-omap-experimental-api \
      --enable-exynos-experimental-api
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}
