# $Id: PKGBUILD 84755 2010-07-04 03:32:01Z eric $
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Mantainer: Roberto Carvajal <roberto@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>>
#  - update to version 2.0.0, something compatible with v4l2
#  - can probably remove this package when upstream catches up

plugrel=1

pkgname=mjpegtools
pkgver=2.0.0
pkgrel=1
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=(i686 x86_64)
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg>=8' 'libpng>=1.4.0' 'sdl' 'gcc-libs' 'libdv')
makedepends=('gtk2>=2.18.6')
optdepends=('gtk2: glav GUI')
options=('!makeflags' '!libtool')
install=mjpegtools.install
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz)
md5sums=('903e1e3b967eebcc5fe5626d7517dc46')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
  ./configure --prefix=/usr --enable-largefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
