# $Id: PKGBUILD 151902 2012-03-03 16:12:24Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make install must be run as -j1, mkdir is not thread-safe for whatever reason

pkgname=help2man
pkgver=1.41.1
pkgrel=1
pkgdesc="Conversion tool to create man files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/help2man/"
license=('GPL')
depends=('perl-locale-gettext')
install=help2man.install
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('4485615cd699a78eb42444324546942a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
	      --infodir=/usr/share/info \
	      --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -j1 DESTDIR="${pkgdir}" install
}
