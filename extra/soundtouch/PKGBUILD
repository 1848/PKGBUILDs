# $Id: PKGBUILD 136035 2011-08-21 23:31:51Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - --with-pic for v6h/v7h

pkgname=soundtouch
pkgver=1.7.1
pkgrel=1
pkgdesc="An audio processing library"
arch=('i686' 'x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('gcc-libs')
options=('!libtool')
source=(http://www.surina.net/soundtouch/${pkgname}-${pkgver}.tar.gz)
sha1sums=('8b1a461754853f9c7328571449b7f9b8bd578a89')

build() {
  cd "${srcdir}/${pkgname}"

  [ "${CARCH}" = "armv6h" -o "${CARCH}" = "armv7h" ] && CONFIGFLAG="--with-pic"
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  ./bootstrap
  ./configure --prefix=/usr --enable-shared ${CONFIGFLAG}
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch install
}
