# $Id: PKGBUILD 183326 2013-04-21 22:06:45Z heftig $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - re-enabled --disable-silent-rules, needed for v5 and v6 to build

pkgbase=webkitgtk
pkgname=('webkitgtk3')
pkgver=2.0.2
pkgrel=1
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
makedepends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3' 'gst-plugins-base-libs' 'gperf' 'gobject-introspection' 'python' 'mesa' 'ruby' 'gtk-doc' 'libsecret' 'libwebp')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/$pkgbase-$pkgver.tar.xz)
sha256sums=('c3685032545eb4c23f3f56826817783a6963ad59bd7bbf806705059b3d8caeb2')

prepare() {
  cd $pkgbase-$pkgver
  mkdir build-gtk3
}

build() {
  cd $pkgbase-$pkgver/build-gtk3

  ../configure --prefix=/usr --libexecdir=/usr/lib/webkitgtk3 --enable-introspection --disable-silent-rules
  make all stamp-po
}

package_webkitgtk3() {
  pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
  depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk3' 'gst-plugins-base-libs' 'libsecret' 'libwebp')
  conflicts=('libwebkit3')
  provides=("libwebkit3=${pkgver}")
  replaces=('libwebkit3')
  optdepends=('gtk2: Webkit2 plugin support')
  
  cd $pkgbase-$pkgver/build-gtk3
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
