# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - re-enabled --disable-silent-rules, needed for v5 and v6 to build

pkgname=webkitgtk
pkgver=2.0.2
pkgrel=3
pkgdesc="GTK+ Web content engine library"
arch=(i686 x86_64)
url="http://webkitgtk.org/"
license=(custom)
depends=(libxt libxslt sqlite libsoup enchant libgl geoclue gtk3 gst-plugins-base-libs libsecret libwebp
         harfbuzz-icu)
makedepends=(gtk2 gperf gobject-introspection python mesa ruby gtk-doc)
optdepends=('gtk2: Webkit2 plugin support')
conflicts=(webkitgtk3 libwebkit3)
provides=("webkitgtk3=${pkgver}" "libwebkit3=${pkgver}")
replaces=(webkitgtk3 libwebkit3)
options=(!libtool !emptydirs)
source=(http://webkitgtk.org/releases/$pkgname-${pkgver}.tar.xz
        changeset_150963.diff)
sha256sums=('c3685032545eb4c23f3f56826817783a6963ad59bd7bbf806705059b3d8caeb2'
            '8d2542600cba876b714cdf6be478d847b6672bff2856e6e6d0582b1566fe5764')

prepare() {
  cd $pkgname-$pkgver
  patch -Np2 -i ../changeset_150963.diff
  autoreconf -fi -I Source/autotools
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/$pkgname \
    --enable-introspection --disable-silent-rules

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make all stamp-po
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
