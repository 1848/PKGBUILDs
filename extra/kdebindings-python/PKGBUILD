# $Id: PKGBUILD 139913 2011-10-05 19:09:37Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

# ALARM
#  - pykde sip patch for arm

plugrel=1

pkgname=kdebindings-python
pkgver=4.7.3
pkgrel=1
pkgdesc="KDE bindings for Python"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdepim-runtime' 'python2-pyqt' 'qscintilla' 'boost-libs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/stable/${pkgver}/src/pykde4-${pkgver}.tar.bz2"
        "pykde4-4.4.4-arm-sip.patch")
sha1sums=('16063793d39fdc9f5b9916cab49236103ce79da2'
          '20d37a8b127eebffc4556352168786036969ee78')

build() {
  cd "${srcdir}/pykde4-${pkgver}"
  patch -Np3 -i "$srcdir/pykde4-4.4.4-arm-sip.patch"
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
