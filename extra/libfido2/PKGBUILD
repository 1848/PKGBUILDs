# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix 32-bit FTBFS

pkgname=libfido2
pkgver=1.5.0
pkgrel=3
pkgdesc='Library functionality for FIDO 2.0, including communication with a device over USB'
url='https://developers.yubico.com/libfido2/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl' 'libcbor' 'libcbor.so' 'hidapi' 'systemd-libs'  'libudev.so')
makedepends=('cmake' 'systemd')
provides=('libfido2.so')
source=(https://developers.yubico.com/libfido2/Releases/libfido2-${pkgver}.tar.gz{,.sig}
        https://github.com/Yubico/libfido2/commit/7a17a4e9127fb6df6278f19396760e7d60a5862c.patch)
sha512sums=('240e2368e43846fddf5e98bbcc247468833565bcde4ec27976b88c814d787f1a477241a82b064818aa0eb0a98ff46a65d80b8243f4d0bbd763270e42492354e2'
            'SKIP'
            'b14a2d2ebb0fa05a3c39fab01c63bb158cd4ab36e307915a48e951088b9550d0728f77250e59f362f15ff7f0e21d6874b9bb228c650bc166202febe15f8b6e64')
b2sums=('23a04f9230c45652aa1ac9fd8b3e809096ae31699c65ca0fda27b27b1b47263e375bb99e5b1ebc515b9edfb801bb2fba9c4f50d88e755efe3eaa23463ca01946'
        'SKIP'
        'a061b7873a53c55812ef48731245f2f34c7972f05d4d0673705f7f21cb520739c198254a8dd7251b8da2f42e5c8934a518fdbda61f2953638efaace7c098334b')
validpgpkeys=(
  'EE90AE0D19774C8386628FAAB428949EF7914718' # pedro martelletto <pedro@yubico.com>
  '1D7308B0055F5AEF36944A8F27A9C24D9588EA0F' # Aveen Ismail <aveen.ismail@yubico.com>
  '7FBB6186957496D58C751AC20E777DD85755AA4A' # Konstantinos Georgantas <kostas@yubico.com>
)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../7a17a4e9127fb6df6278f19396760e7d60a5862c.patch
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DUDEV_RULES_DIR=/usr/lib/udev/rules.d
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
