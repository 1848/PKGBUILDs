# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - add proposed upstream patches to fix Elan I2C touchpads

pkgname=libinput
pkgver=1.7.0
pkgrel=1.1
pkgdesc="Input device management and event handling library"
arch=(i686 x86_64)
url="https://www.freedesktop.org/wiki/Software/libinput/"
license=(custom:X11)
depends=('mtdev' 'systemd' 'libevdev' 'libwacom')
# currently no doc files to install
makedepends=('doxygen' 'graphviz' 'gtk3')
#checkdepends=('check' 'libunwind')
source=(https://freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        0001-touchpad-move-the-pressure-range-to-a-hwdb-entry.patch
        0002-udev-Add-name-based-input-device-detection-without-d.patch
        0003-udev-Add-support-for-I2C-Elan-touchpads-without-dmi.patch
        0004-udev-Decrease-high-pressure-value-for-Elantech-touch.patch)
sha512sums=('9058eab813ea3de230835155ca843f248127cbafaf1aecc9a2e209a0215b090beef0468cc863a24320f8d0db1f2863baba680e2416e9e409e958b2c1d18e43a1'
            'SKIP'
            '96a368290c0d2993da2d4527546642345a41127dcd4ed52908c252086884f5107f026817c1e4c8abccc73c5dbfcd80fb5295685196795f9606b28dafe4b60576'
            '2b9c903ad01c14a834dceef0cb8d889fc9f79e52517076e0ee7b3396d0647d10c11a4e4ad0615cbd37f6272e3bee5cd37681d3436786f850b24171fdd99742c5'
            '031b3de6bf9201313028ceaa889580fdf43a63a520d205a61c77e1bb0c72decca69bf0a6a2e966e11098444629ca02328afdc636f4dac28106bbe32babcf93a6'
            '6c1201a4bf64facb8384bbee3c1cd86c7b522332f6d105ea316bfbba18f3e740257527ba3b657952543dcf3e107406cf49f1f9b9e66963fd3272d7bce37bcacd')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-touchpad-move-the-pressure-range-to-a-hwdb-entry.patch
  patch -p1 -i ../0002-udev-Add-name-based-input-device-detection-without-d.patch
  patch -p1 -i ../0003-udev-Add-support-for-I2C-Elan-touchpads-without-dmi.patch
  patch -p1 -i ../0004-udev-Decrease-high-pressure-value-for-Elantech-touch.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
# disabled for now:
# https://github.com/libcheck/check/issues/18
#  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  # install doc - no Makefile target
  install -v -dm755 ${pkgdir}/usr/share/doc/libinput
  cp -rv doc/html/* ${pkgdir}/usr/share/doc/libinput
}
