# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patch to explicitly link v5/v6 with libatomic

pkgname=kdiff3
pkgver=1.9.0
pkgrel=2
pkgdesc='A file comparator/merge tool'
arch=(x86_64)
url='https://kde.org/applications/development/kdiff3/'
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        0001-link-atomic.patch)
sha256sums=('a7fd61a23f1e37f25af0efa82265d4723e63889aa3213a58b76ce0c96eab23d0'
            'SKIP'
            '47ce388156ae3d70efb6915c94db3502b0bbdfa439aaf4a6b75f7ab6702e338c')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

prepare() {
  if [[ $CARCH == "armv6h" || $CARCH == "arm" ]]; then
    cd $pkgname-$pkgver
    patch -p1 -i ../0001-link-atomic.patch
  fi
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DNDEBUG"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
