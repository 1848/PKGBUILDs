# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - upstream patch to fix aarch64 FTBFS

pkgname=libuv
pkgver=1.33.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc' 'libnsl')
makedepends=('python-sphinx')
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        'https://github.com/libuv/libuv/commit/83f59ff1c121f3a872041f060dd6994aca4a279e.patch')
sha512sums=('0b0767d9743931060fe9347ca239d8fb3192a2e0182101dc8f55ab3c9a7440967eda062cc2b3b8b1aede4bdda690e5c20e55ad258c273a1518412e08bf8b8707'
            'e75bcefcb1231c3f88e28b74c5ce77688b4a4d8517ce2c5d525f65fd3897a7970a9f26f796b866329af8b4f703ae11add79140c20197d4e5da2b5fc08c32b9b2')

prepare() {
	cd $pkgname-$pkgver
	patch -p1 -i ../83f59ff1c121f3a872041f060dd6994aca4a279e.patch
}

build() {
	cd $pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr
	make
	make man -C docs
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 AUTHORS \
		"$pkgdir"/usr/share/doc/$pkgname/AUTHORS
	install -Dm644 README.md \
		"$pkgdir"/usr/share/doc/$pkgname/README.md
	install -Dm644 ChangeLog \
		"$pkgdir"/usr/share/doc/$pkgname/ChangeLog
	install -Dm644 docs/build/man/libuv.1 \
		"$pkgdir"/usr/share/man/man1/libuv.1
}
