# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - disable functional tests

pkgname=tracker
pkgver=2.3.0
pkgrel=1
pkgdesc="Desktop-neutral user information store, search tool and indexer"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(sqlite icu glib2 libffi util-linux libstemmer libseccomp libsoup json-glib upower libnm)
makedepends=(gobject-introspection vala git gtk-doc bash-completion meson)
checkdepends=(python-gobject python-dbus)
groups=(gnome)
_commit=f56ccfde3b0a63375d6a4a664928b348c9ea45eb  # tags/2.3.0^0
source=("git+https://gitlab.gnome.org/GNOME/tracker.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  # LTO breaks due to duplicate types:
  # https://gitlab.gnome.org/GNOME/tracker/issues/44
  arch-meson $pkgname build -D docs=true -D functional_tests=false
  ninja -C build
}

check() {
  # Many tests are broken:
  # - Assume schemas are installed
  # - Assume services are autolaunched
  dbus-run-session meson test -C build --print-errorlogs -t 3 || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
