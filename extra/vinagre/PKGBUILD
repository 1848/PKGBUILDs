# $Id: PKGBUILD 142897 2011-11-17 18:35:11Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

# ALARM: Kevin Mihelich
#  - commented re-exporting LDFLAGS change, breaks stuff for us.

plugrel=1

pkgname=vinagre
pkgver=3.2.2
pkgrel=1
pkgdesc="A VNC Client for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/vinagre/"
install=vinagre.install
depends=('libgnome-keyring' 'gtk-vnc' 'vte3' 'telepathy-glib' 'avahi' 'desktop-file-utils' 'dconf' 'shared-mime-info' 'gnome-icon-theme')
optdepends=('openssh: SSH plugin'
            'rdesktop: RDP plugin')
makedepends=('gnome-doc-utils' 'intltool' 'rdesktop' 'openssh')
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('1533cc447177f8d6c9cfe45d61b05904efbf66c86b86b4c332e972ff965d76bd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #building with as-needed breaks rdp
  #https://bugzilla.gnome.org/show_bug.cgi?id=653558
  #export LDFLAGS="$LDFLAGS,--no-as-needed"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-scrollkeeper \
      --libexecdir=/usr/lib/vinagre --enable-rdp --disable-spice
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
