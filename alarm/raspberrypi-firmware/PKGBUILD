buildarch=20

pkgbase=raspberrypi-firmware
pkgname=('raspberrypi-firmware'
         'raspberrypi-firmware-bootloader'
         'raspberrypi-firmware-bootloader-x'
         'raspberrypi-firmware-tools'
         'raspberrypi-firmware-examples')
pkgver=20160704
pkgrel=1
_commit=1f8d685c22761b9a5ee3343b01e79d31e2ed0f6b
pkgdesc="Firmware files for Raspberry Pi"
arch=('any')
url="https://github.com/raspberrypi/firmware"
makedepends=('git')
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz"
        '00-raspberrypi-firmware.conf')
md5sums=('aab212972d04d84c11761e50b3110646'
         'baeeace452010ff9a936fb1ea33863a8')

package_raspberrypi-firmware() {
  depends=('raspberrypi-firmware-bootloader'
           'raspberrypi-firmware-bootloader-x'
           'raspberrypi-firmware-tools'
           'raspberrypi-firmware-examples')
}

package_raspberrypi-firmware-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-firmware-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}

package_raspberrypi-firmware-tools() {
  pkgdesc="Firmware tools and header files for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/{bin,include,lib,sbin,LICENCE} "${pkgdir}"/opt/vc

  # move GL libraries out of the way due to Mesa incompatibility
  mkdir -p "${pkgdir}/opt/vc/lib/GL"
  mv "${pkgdir}/opt/vc/lib"/{libGL*,libEGL*} "${pkgdir}/opt/vc/lib/GL"
  sed -i 's|/lib|/lib/GL|' "${pkgdir}/opt/vc/lib/pkgconfig"/{egl.pc,glesv2.pc}

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
  cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
}

package_raspberrypi-firmware-examples() {
  pkgdesc="Firmware example sources for Raspberry Pi"

  mkdir -p "${pkgdir}"/opt/vc

  cp -R "${srcdir}"/firmware-${_commit}/hardfp/opt/vc/src "${pkgdir}"/opt/vc
}
