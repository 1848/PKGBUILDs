buildarch=18

pkgname=raspberrypi-firmware
pkgver=20130218
pkgrel=1
pkgdesc="Firmware files for Raspberry Pi"
arch=('any')
url="https://github.com/raspberrypi/firmware"
makedepends=('git')
license=('custom')
options=(!strip)
_gitroot=git://github.com/raspberrypi/firmware.git
_gitname=firmware
provides=('raspberrypi-firmware')
conflicts=('raspberrypi-firmware')
build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"

  rm -f "${srcdir}"/firmware/boot/kernel.img
}

package() {
  cp -R "${srcdir}"/firmware/boot "${pkgdir}"/boot
  mkdir -p "${pkgdir}"/opt/vc

  if [[ $CARCH == "arm" ]]; then
    cp -R "${srcdir}"/firmware/opt/vc/{bin,include,lib,sbin,src,LICENCE} "${pkgdir}"/opt/vc
  elif [[ $CARCH == "armv6h" ]]; then
    cp -R "${srcdir}"/firmware/hardfp/opt/vc/{bin,include,lib,sbin,src,LICENCE} "${pkgdir}"/opt/vc
  fi

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
 
  # ensure we can load libs
  echo "/opt/vc/lib/" > "${pkgdir}"/etc/ld.so.conf.d/raspberrypi-firmware.conf
}
