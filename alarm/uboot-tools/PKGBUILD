# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Contributor: Jonny Gerold <jonny@fsk141.com>
# Contributor: Julian Langschaedel <meta.rb@gmail.com>

pkgname=uboot-tools
pkgver=2014.10
pkgrel=1
pkgdesc="U-Boot Tools"
arch=('arm' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
provides=('uboot-env' 'uboot-mkimage')
replaces=('uboot-env' 'uboot-mkimage')
backup=(etc/fw_env.config)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2
	fw_env.config)
md5sums=('3ddcaee2f05b7c464778112ec83664b5'
         '159f285467e910ba98696b92a311498e')

build() {
  cd u-boot-${pkgver}

  touch include/config.{h,mk} include/autoconf.mk{.dep,}

  make defconfig
  make silentoldconfig
  make tools-only
  make SUBDIRS=tools env
  make -C tools kwboot
}

package() {
  cd u-boot-${pkgver}

  install -dm755 ${pkgdir}/{usr/{bin,share/man/man1},etc}
  install -m755 tools/{dumpimage,mkimage,mkenvimage,proftool,env/fw_printenv,kwboot} ${pkgdir}/usr/bin
  ln -s /usr/bin/fw_printenv ${pkgdir}/usr/bin/fw_setenv
  install -m644 ${srcdir}/fw_env.config ${pkgdir}/etc
  install -m644 doc/{mkimage,kwboot}.1 ${pkgdir}/usr/share/man/man1
}
