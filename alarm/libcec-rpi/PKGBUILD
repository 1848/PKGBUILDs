# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Philippe Cherel <philippe.cherel@mayenne.org>

buildarch=16

pkgname=libcec-rpi
pkgver=2.1.3
pkgrel=1
pkgdesc="Pulse-Eight's libcec for the Pulse-Eight USB-CEC adapter with support for raspberry pi"
arch=('armv6h')
url="https://github.com/Pulse-Eight/libcec"
license=('GPL')
depends=('udev' 'lockdev' 'raspberrypi-firmware')
conflicts=('libcec')
provides=('libcec')
source=("https://github.com/Pulse-Eight/libcec/archive/libcec-$pkgver.tar.gz" "fix-boot_t.patch")
sha256sums=('2aa88451b528184b02077ee8c6cd10e2f89121a6a05b1b35b4b792b03108a9d1'
            'ba9b4030f3c2aa092a7c513629b60e82eeca7daf044576fa89b117409a8e883f')
options=(!libtool)

build() {

  cd "libcec-libcec-$pkgver"
  export CFLAGS="$CFLAGS -fPIC"
  export CXXFLAGS="$CXXFLAGS -fPIC"
  export LDFLAGS="$LDFLAGS -fPIC"
  patch -p1 -i ../fix-boot_t.patch
  autoreconf -vif
  ./configure --prefix=/usr --enable-rpi \
   --with-rpi-include-path="/opt/vc/include" \
   --with-rpi-lib-path="/opt/vc/lib" 
  make
}

package() {
  cd "libcec-libcec-$pkgver"
  make DESTDIR="$pkgdir" install
}

