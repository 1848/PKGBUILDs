# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20130408
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.gz.sig"
        '0001-makechrootpkg-cache-dir.patch'
        '0002-mkarchroot-dont-change-confs.patch'
        '0003-makechrootpkg-distcc.patch')
md5sums=('12b42b429744af6517ae789f728e9f63'
         'SKIP'
         '1a85c2d42aa9aaa77847e303422bc11e'
         '37d9d2ca96a3a5130d39b1441b7acb99'
         '550d15ec4ee1803476424d60460b89ec')

prepare() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch -p1 -i "${srcdir}"/0001-makechrootpkg-cache-dir.patch
	patch -p1 -i "${srcdir}"/0002-mkarchroot-dont-change-confs.patch
	patch -p1 -i "${srcdir}"/0003-makechrootpkg-distcc.patch
}

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
