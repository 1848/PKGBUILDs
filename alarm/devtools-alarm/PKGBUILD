# $Id: PKGBUILD 182399 2013-04-10 05:34:09Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - patches to fix issues with how we use the tools

pkgname=devtools-alarm
_pkgname=devtools
pkgver=20171108
pkgrel=2.1
pkgdesc='Tools for Arch Linux ARM package maintainers'
arch=('any')
license=('GPL')
url='http://projects.archlinux.org/devtools.git/'
conflicts=('devtools')
provides=('devtools')
depends=('namcap' 'openssh' 'subversion' 'rsync' 'arch-install-scripts')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'add-etc-shadow-entry-for-builduser.patch'
        '0001-makechrootpkg-cache-dir.patch'
        '0002-arch-nspawn-keep-mirrorlist.patch'
        '0003-makechrootpkg-distcc.patch'
        '0004-arch-nspawn-arm-fix.patch'
        '0005-makechrootpkg-no-default-logging.patch'
        '0006-archbuild-no-setarch.patch'
        '0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch'
        '0008-makechrootpkg-Fix-unconditionally-running-namcap.patch'
        '0009-makechrootpkg-fix-verifysource-with-pacman-git.patch')
md5sums=('4a24b99bc1cffc63634eb7b3f5b116f1'
         '573830ee23ecffa4971fbc36e5e2b666'
         '6f0857c607eecd7cae37395cf28a6c81'
         'c73c9aa27af9bbcb8649c476a33cce6e'
         '28bca7b3fe32ea5236e5dfbccf51551e'
         '3007724b09d64626d949d698cb5a0816'
         '5ba5b5da4a4a0d37c4651c48241589d2'
         '0664e294a68e343c29e3ec01bfdd189c'
         '048314c00ef274602c8fc830db63c61a'
         '9b915424e24d3245fd9786133bc203c1'
         '1dd4421374a9a661472e2139573b6364')

prepare() {
	cd "${_pkgname}-${pkgver}"
	patch -Np1 -i ../add-etc-shadow-entry-for-builduser.patch
	patch -p1 -i ../0001-makechrootpkg-cache-dir.patch
	patch -p1 -i ../0002-arch-nspawn-keep-mirrorlist.patch
	patch -p1 -i ../0003-makechrootpkg-distcc.patch
	patch -p1 -i ../0004-arch-nspawn-arm-fix.patch
	patch -p1 -i ../0005-makechrootpkg-no-default-logging.patch
	patch -p1 -i ../0006-archbuild-no-setarch.patch
	patch -p1 -i ../0007-makechrootpkg-don-t-delete-MAKEFLAGS-and-PACKAGER.patch
	patch -p1 -i ../0008-makechrootpkg-Fix-unconditionally-running-namcap.patch
	patch -p1 -i ../0009-makechrootpkg-fix-verifysource-with-pacman-git.patch
}

build() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr
}

package() {
	cd "${_pkgname}-${pkgver}"
	make PREFIX=/usr DESTDIR=${pkgdir} install
}
