# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgname=firmware-raspberrypi
_firmcommit=b66ab26cebff689d0d3257f56912b9bb03c20567
_bluecommit=09eeca34619a1691ad2983d355a9301db75f0456
pkgver=20201225
pkgrel=1
pkgdesc="Additional firmware for Raspberry Pi"
arch=('any')
url="https://github.com/RPi-Distro/firmware-nonfree/commits/master/brcm"
license=('custom')
options=('!strip')
source=("nonfree-$pkgver-${_firmcommit:0:10}.tar.gz::https://github.com/RPi-Distro/firmware-nonfree/archive/${_firmcommit}.tar.gz"
        "bluez-$pkgver-${_bluecommit:0:10}.tar.gz::https://github.com/RPi-Distro/bluez-firmware/archive/${_bluecommit}.tar.gz"
        'RPi-WM8804.conf')
sha256sums=('033a21d19fbdc7617b8c5b58d4be5951e29be5be787a45875b615f4d4dcf3f5b'
            '1e9d81592f18b3288f53b83e652fa2139100012d3c74974f63b013ff510e791f'
            'f978fbc40db75ba3213a4472023496d0716706eb1a6f078f207ac027c5753f43')

package() {
  install -d "$pkgdir/usr/lib/firmware/updates/brcm" "$pkgdir/usr/share/alsa/cards/"
  install -m 0644 RPi-WM8804.conf "$pkgdir/usr/share/alsa/cards/"

  _bluez=(
    BCM43430A1.hcd
    BCM4345C0.hcd
  )
  for i in "${_bluez[@]}"; do
    install -m 0644 "bluez-firmware-$_bluecommit/broadcom/$i" "$pkgdir/usr/lib/firmware/updates/brcm/$i"
  done

  # BCM43430 on Zero/3A/3B
  # BCM43455 on 3A+/3B+/4B/CM4
  # BCM43456 on 400

  _payload=(
    brcmfmac43430-sdio.bin
    brcmfmac43430-sdio.txt
    brcmfmac43455-sdio.bin
    brcmfmac43455-sdio.clm_blob
    brcmfmac43455-sdio.txt
    brcmfmac43456-sdio.bin
    brcmfmac43456-sdio.clm_blob
    brcmfmac43456-sdio.txt
  )
  for i in "${_payload[@]}"; do
    install -m 0644 "firmware-nonfree-$_firmcommit/brcm/$i" "$pkgdir/usr/lib/firmware/updates/brcm/$i"
  done
}
