# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>
# Maintainer: graysky <graysky@archlnux.us>

buildarch=28

pkgname=firmware-raspberrypi
_firmcommit=f713a6054746bc61ece1c8696dce91a7b7e22dd9
pkgver=20210220
pkgrel=1
pkgdesc="Additional firmware for Raspberry Pi"
arch=('any')
url="https://github.com/RPi-Distro/firmware-nonfree/commits/master/brcm"
license=('custom')
options=('!strip')
source=(
        # bluetooth for RPi3+
        'https://archlinuxarm.org/builder/src/bcm43430/BCM43430A1.hcd'
        'https://archlinuxarm.org/builder/src/bcm43455/BCM4345C0.hcd'
        # BCM43455 on RPi-3A+/3B+/4B/CM4
        'brcmfmac43455-sdio.raspberrypi,4-model-b.txt'
        # dropin to fix WiFi on Zero-W
        'https://archlinuxarm.org/builder/src/bcm43430/7.45.98.38/brcmfmac43430-sdio.txt'
        # BCM43456 on RPi-400
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.bin"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.clm_blob"
        "https://github.com/RPi-Distro/firmware-nonfree/raw/$_firmcommit/brcm/brcmfmac43456-sdio.txt"
        'RPi-WM8804.conf')

sha256sums=('8dd70b9003d39cb6175b4f3cd509666dad66ad23d3be1a68385817fb814c8930'
            '1788c58ecedd61b380e0dd120844ed61e5fbbea0a58e84e6293685eb3384fddc'
            'edb6f4e4fb19e18940004124feb4ffe160d72fc607243a07a4480338a28b2748'
            'e4ce7ad5ce72406f958ed758539b65d958d1c0eb3e46e3f8e9f02b510d88e0b0'
            'ddf83f2100885b166be52d21c8966db164fdd4e1d816aca2acc67ee9cc28d726'
            '2dbd7d22fc9af0eb560ceab45b19646d211bc7b34a1dd00c6bfac5dd6ba25e8a'
            '44e0bb322dc1f39a4b0a89f30ffdd28bc93f7d7aaf534d06d229fe56f6198194'
            'f978fbc40db75ba3213a4472023496d0716706eb1a6f078f207ac027c5753f43')

package() {
  install -d "$pkgdir/usr/lib/firmware/updates/brcm" "$pkgdir/usr/share/alsa/cards/"
  install -m 0644 -- *.bin *.txt *.clm_blob *.hcd "${pkgdir}/usr/lib/firmware/updates/brcm"
  install -m 0644 RPi-WM8804.conf "$pkgdir/usr/share/alsa/cards/"
}
