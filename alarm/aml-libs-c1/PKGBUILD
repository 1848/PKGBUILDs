# ODROID-C1 Amlogic libraries
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgname=aml-libs-c1
pkgver=r11.820b12a
pkgrel=1
arch=('armv7h')
pkgdesc="ODROID-C1 Amlogic Libraries"
url="http://www.hardkernel.com/"
license=('Proprietary')
depends=('alsa-lib')
makedepends=('git')
source=("git+https://github.com/mdrjr/c1_aml_libs.git")
md5sums=('SKIP')

pkgver() {
  cd c1_aml_libs
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd c1_aml_libs
  make clean
  make
}

package() {
  cd c1_aml_libs
  install -d "${pkgdir}/usr/include"
  install -d "${pkgdir}/etc/ld.so.conf.d"
  make DESTDIR="${pkgdir}" install
}
