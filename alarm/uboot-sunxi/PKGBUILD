# U-Boot: sunXi
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=4

pkgbase=uboot-sunxi
pkgname=('uboot-cubieboard2' 'uboot-cubietruck' 'uboot-iteaduino_plus_a10' 'uboot-iteaduino_plus_a20')
pkgver=2014.01
pkgrel=4
arch=('armv7h')
url="https://github.com/linux-sunxi/u-boot-sunxi/tree/sunxi-current"
license=('GPL')
makedepends=('sunxi-tools')
backup=(boot/uEnv.txt)
_commit=765936d2c8ebf048b9c147fc981beccd115c6158
source=("https://github.com/linux-sunxi/u-boot-sunxi/archive/${_commit}.tar.gz"
        'alarm.patch'
        'cubieboard2.fex' 'cubieboard2.env'
        'cubietruck.fex' 'cubietruck.env'
        'iteaduino_plus_a10.fex' 'iteaduino_plus_a10.env'
        'iteaduino_plus_a20.fex' 'iteaduino_plus_a20.env' )
md5sums=('8e4f2a2c1384f31eca5d3864b86e8351'
         'd14a621663c56d37251a3d42d1d10236'
         '36c04988cecd53151f3f5ff5c48d76a9'
         'd41d8cd98f00b204e9800998ecf8427e'
         'c898ab1b57f474d620f5704b2a53d87c'
         'd41d8cd98f00b204e9800998ecf8427e'
         '1bd3355ad6c6c6976c664223958db3f4'
         'd41d8cd98f00b204e9800998ecf8427e'
         '4b85ce18f33c9b4a6338fa973df44f54'
         'd41d8cd98f00b204e9800998ecf8427e')

prepare() {
  cd u-boot-sunxi-${_commit}
  patch -p1 -i "${srcdir}"/alarm.patch
}

build() {
  cd u-boot-sunxi-${_commit}

  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS

  for i in Cubieboard2 Cubietruck Iteaduino_Plus_A10 Iteaduino_Plus_A20; do
    mkdir ../bin_${i}
    make distclean
    make ${i}_config
    make
    mv u-boot-sunxi-with-spl.bin ../bin_${i}
  done
}

package_uboot-cubieboard2() {
  pkgdesc="U-Boot for Cubieboard 2"
  install=${pkgbase}.install
  conflicts=('uboot-cubietruck' 'uboot-iteaduino_plus_a10' 'uboot-iteaduino_plus_a20')

  mkdir -p "${pkgdir}"/boot
  cp bin_Cubieboard2/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin cubieboard2.fex "${pkgdir}"/boot/script.bin
  cp cubieboard2.env "${pkgdir}"/boot/uEnv.txt
}

package_uboot-cubietruck() {
  pkgdesc="U-Boot for Cubietruck"
  install=${pkgbase}.install
  conflicts=('uboot-cubieboard2' 'uboot-iteaduino_plus_a10' 'uboot-iteaduino_plus_a20')

  mkdir -p "${pkgdir}"/boot
  cp bin_Cubietruck/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin cubietruck.fex "${pkgdir}"/boot/script.bin
  cp cubietruck.env "${pkgdir}"/boot/uEnv.txt
}

package_uboot-iteaduino_plus_a10() {
  pkgdesc="U-Boot for Iteaduino Plus A10"
  install=${pkgbase}.install
  conflicts=('uboot-cubieboard2' 'uboot-cubietruck' 'uboot-iteaduino_plus_a20') 

  mkdir -p "${pkgdir}"/boot
  cp bin_Iteaduino_Plus_A10/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin iteaduino_plus_a10.fex "${pkgdir}"/boot/script.bin
  cp iteaduino_plus_a10.env "${pkgdir}"/boot/uEnv.txt
}

package_uboot-iteaduino_plus_a20() {
  pkgdesc="U-Boot for Iteaduino Plus A20"
  install=${pkgbase}.install
  conflicts=('uboot-cubieboard2' 'uboot-cubietruck' 'uboot-iteaduino_plus_a10')

  mkdir -p "${pkgdir}"/boot
  cp bin_Iteaduino_Plus_A20/u-boot-sunxi-with-spl.bin "${pkgdir}"/boot

  fex2bin iteaduino_plus_a20.fex "${pkgdir}"/boot/script.bin
  cp iteaduino_plus_a20.env "${pkgdir}"/boot/uEnv.txt
}

