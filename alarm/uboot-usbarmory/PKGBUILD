# U-Boot: Inverse Path USB armory
# Maintainer: Jason Plum <jplum@archlinuxarm.org>

buildarch=4

pkgname=uboot-usbarmory
pkgver=2014.07
pkgrel=1
pkgdesc="U-Boot for USB armory"
arch=('armv7h')
url="https://github.com/inversepath/u-boot-usbarmory.git"
license=('GPL')
makedepends=('bc')
source=("u-boot-${pkgver}::git+https://github.com/inversepath/u-boot-usbarmory.git"
        'alarm.patch'
        'uEnv.txt')
md5sums=('SKIP'
         'f21fec4a935f94f16dff8d48b2a2501e'
         'd41d8cd98f00b204e9800998ecf8427e')

prepare() {
  cd u-boot-${pkgver}
  patch -p1 -i ../alarm.patch
}

build() {
  cd u-boot-${pkgver}

  unset CFLAGS
  unset CXXFLAGS

  make distclean
  make usbarmory_config
  make
}

package() {
  cd u-boot-${pkgver}
  mkdir -p "${pkgdir}"/boot
  cp u-boot.imx "${srcdir}"/uEnv.txt "${pkgdir}"/boot
}
