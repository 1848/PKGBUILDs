# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader'
         'raspberrypi-bootloader-x')
pkgver=20200928
pkgrel=1
_commit=f0eab3a24f42c06fb6f2360140a00873c2793b1f
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('050e4e3370a3c68e50ab97bf4a0cefe8')

package_raspberrypi-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start{_,4}x.elf,fixup{_,4}x.dat}
}

package_raspberrypi-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start{_,4}x.elf,fixup{_,4}x.dat} "${pkgdir}"/boot
}
