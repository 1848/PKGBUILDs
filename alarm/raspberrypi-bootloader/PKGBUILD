# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader'
         'raspberrypi-bootloader-x')
pkgver=20180314
pkgrel=1
_commit=086a8488de0f485e8879ac14ff505ff1d922eeed
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('f714117194d6c45eefa932aed2969a86')

package_raspberrypi-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}
