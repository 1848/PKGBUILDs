# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=28

pkgbase=raspberrypi-bootloader
pkgname=('raspberrypi-bootloader'
         'raspberrypi-bootloader-x')
pkgver=20180910
pkgrel=1
_commit=70c60c5c57d9d639fbd92276f18558ada51b7c53
arch=('any')
url="https://github.com/raspberrypi/firmware"
license=('custom')
options=(!strip)
source=("https://github.com/raspberrypi/firmware/archive/${_commit}.tar.gz")
md5sums=('e6b9ad41b1c8d9c7c5e3c41ac71ec9c7')

package_raspberrypi-bootloader() {
  pkgdesc="Bootloader files for Raspberry Pi"
  replaces=('raspberrypi-firmware-bootloader')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{*.dat,*.bin,*.elf} "${pkgdir}"/boot
  rm "${pkgdir}"/boot/{start_x.elf,fixup_x.dat}
}

package_raspberrypi-bootloader-x() {
  pkgdesc="Bootloader with extra codecs for Raspberry Pi"
  depends=('raspberrypi-bootloader')
  replaces=('raspberrypi-firmware-bootloader-x')

  mkdir -p "${pkgdir}"/boot
  cp "${srcdir}"/firmware-${_commit}/boot/{start_x.elf,fixup_x.dat} "${pkgdir}"/boot
}
