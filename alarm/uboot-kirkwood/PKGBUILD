# U-Boot: Marvell Kirkwood platforms
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=2

pkgbase=uboot-kirkwood
pkgname=('uboot-dockstar' 'uboot-goflexhome' 'uboot-goflexnet')
pkgver=2014.04.R2
pkgrel=1
arch=('arm')
url="https://github.com/archlinuxarm/u-boot"
license=('GPL')
depends=('uboot-env' 'mtd-utils')
source=("https://github.com/archlinuxarm/u-boot/archive/${pkgver##*.}.tar.gz"
        'http://archlinuxarm.org/os/armv5te/boot/dockstar/uboot-dockstar.env'
        'http://archlinuxarm.org/os/armv5te/boot/goflexhome/uboot-goflexhome.env'
        'http://archlinuxarm.org/os/armv5te/boot/goflexhome/uboot-goflexnet.env')
md5sums=('d9c20346e94f8e05d168b43fdc935bea'
         '483bb63ff84c05d96e43ec4c85b8d7c2'
         'b9c1acb44e25a45767f31da1f9fd0261'
         '88197b33fee74a31d4914ca4f14007ec')

build() {
  cd u-boot-${pkgver##*.}
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  mkdir bins
  for i in dockstar goflexhome goflexnet; do
    make distclean
    make ${i}_config
    make u-boot.kwb EXTRAVERSION=.${pkgver##*.}-${pkgrel}
    dd if=u-boot.kwb of=bins/uboot-${i}.kwb bs=512k conv=sync
  done;
}

package_uboot-dockstar() {
  pkgdesc="U-Boot for the Seagate FreeAgent DockStar"
  install="$pkgname.install"

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver##*.}/bins/uboot-dockstar.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-dockstar.env "${pkgdir}"/boot
}

package_uboot-goflexhome() {
  pkgdesc="U-Boot for the Seagate GoFlex Home"
  install="$pkgname.install"

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver##*.}/bins/uboot-goflexhome.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-goflexhome.env "${pkgdir}"/boot
}

package_uboot-goflexnet() {
  pkgdesc="U-Boot for the Seagate GoFlex Net"
  install="$pkgname.install"

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver##*.}/bins/uboot-goflexnet.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-goflexnet.env "${pkgdir}"/boot
}
