# U-Boot: Marvell Kirkwood platforms
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=2

pkgbase=uboot-kirkwood
pkgname=('uboot-dockstar' 'uboot-goflexhome')
pkgver=2014.04.R1
pkgrel=1
arch=('arm')
url="https://github.com/archlinuxarm/u-boot"
license=('GPL')
depends=('uboot-env' 'mtd-utils')
source=("https://github.com/archlinuxarm/u-boot/archive/${pkgver##*.}.tar.gz"
        'http://archlinuxarm.org/os/armv5te/boot/dockstar/uboot-dockstar.env'
        'http://archlinuxarm.org/os/armv5te/boot/goflexhome/uboot-goflexhome.env')
md5sums=('b94af10e66bee284aea93cfc2b919dc5'
         '483bb63ff84c05d96e43ec4c85b8d7c2'
         'b9c1acb44e25a45767f31da1f9fd0261')

build() {
  cd u-boot-${pkgver##*.}
  
  unset CFLAGS CXXFLAGS LDFLAGS
  
  mkdir bins
  for i in dockstar goflexhome; do
    make distclean
    make ${i}_config
    make u-boot.kwb EXTRAVERSION=.${pkgver##*.}-${pkgrel}
    dd if=u-boot.kwb of=bins/uboot-${i}.kwb bs=512k conv=sync
  done;
}

package_uboot-dockstar() {
  pkgdesc="U-Boot for the Seagate FreeAgent DockStar"
  install="$pkgname.install"

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver##*.}/bins/uboot-dockstar.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-dockstar.env "${pkgdir}"/boot
}

package_uboot-goflexhome() {
  pkgdesc="U-Boot for the Seagate GoFlex Home"
  install="$pkgname.install"

  mkdir -p "${pkgdir}"/boot
  cp u-boot-${pkgver##*.}/bins/uboot-goflexhome.kwb "${pkgdir}"/boot
  cp "${srcdir}"/uboot-goflexhome.env "${pkgdir}"/boot
}
