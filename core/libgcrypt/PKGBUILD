# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - Note: this package needs to be fully re-checked beyond version 1.6.0
#
#  - switch to git repo to pull in missing ARM assembly files and fixes
#  - switch ifdef's in cipher files to actually respect --disable-neon-support
#  - disable intel's RNG
#  - drop docs from building in the makefile, all sorts of busted in git
#  - disable .install so it doesn't try to install nonexistent docs

pkgname=libgcrypt
pkgver=1.6.0
pkgrel=1.1
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.10-2')
makedepends=('git')
options=('!emptydirs')
#install=$pkgname.install
source=("${pkgname}-${pkgver}::git://git.gnupg.org/libgcrypt.git#branch=LIBGCRYPT-1-6-BRANCH")
sha1sums=('SKIP')

build() {
  cd ${pkgname}-${pkgver}

  sed -i 's/HAVE_GCC_INLINE_ASM_NEON/ENABLE_NEON_SUPPORT/g' cipher/*
  sed -i 's/ doc / /g' Makefile.am
  autoreconf -fi

  ./configure --prefix=/usr \
	--disable-static \
	--disable-padlock-support \
	--disable-drng-support \
	--disable-neon-support
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
