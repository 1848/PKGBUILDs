# $Id: PKGBUILD 142067 2011-11-04 23:35:59Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - rebuild because goddamn upstream built theirs statically linked, didn't trigger rebuild
#    for libsigsegv.. STOP DOING THIS IF IT'S NOT IN THE PKGBUILD

plugrel=1

pkgname=m4
pkgver=1.4.16
pkgrel=2.1
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
install=m4.install
source=(ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig}
        m4-1.4.16-readlink-einval.patch)
md5sums=('7548ec061a1ba993790159764f522d0e'
         'eebe5c94e74e7551e2e30c5844f1b653'
         'cea138fa9b568d06e46269611cec8dd0')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np1 -i $srcdir/m4-1.4.16-readlink-einval.patch
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
