# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - inittab serial setup for v5/v7
#  - patch to adjust naming to ALARM, fix remounting root, turn off LEDs on shutdown

plugrel=1

pkgname=initscripts
pkgver=2012.05.1
pkgrel=3
pkgdesc="System initialization/bootup scripts"
arch=('any')
url="http://www.archlinux.org"
license=('GPL2')
groups=('base')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless)
depends=('glibc' 'bash' 'grep' 'coreutils' 'systemd-tools' 'iproute2'
         'ncurses' 'kbd' 'findutils' 'sysvinit')
optdepends=('bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'net-tools: legacy network support'
            'wireless_tools: Wireless networking')
makedepends=(asciidoc)
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/${pkgname}-${pkgver}.tar.xz"
        'wireless.conf.d'
        'alarm.patch'
        'rc.conf')
md5sums=('13cff6a6638f12351892d7085d7e6fb9'
         '027576534885b8d5dded9be546057b12'
         'e77890fd546ca599a2bd129b040c0b05'
         'c618e099f522f861e08a475f8960a6c2')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}/alarm.patch"
  cp "${srcdir}/rc.conf" .
  [ "${CARCH}" == "arm" ]    && sed -i 's/@TTY@/ttyS0/' inittab
  [ "${CARCH}" == "armv7h" ] && sed -i 's/@TTY@/ttyO2/' inittab

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless

  # these moved to systemd-tools, will be gone in the next initscripts release
  rm ${pkgdir}/usr/share/man/man5/{hostname,{locale,vconsole}.conf}.5
}
