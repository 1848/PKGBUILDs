# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - inittab serial setup for v5/v7
#  - patch to adjust naming to ALARM, turn off LEDs on shutdown

pkgname=initscripts
pkgver=2012.06.3
pkgrel=2
pkgdesc="System initialization/bootup scripts"
arch=('any')
url="http://www.archlinux.org"
license=('GPL2')
groups=('base')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless)
depends=('glibc' 'bash' 'coreutils' 'systemd-tools' 'iproute2' 'ncurses' 'findutils' 'sysvinit')
optdepends=('bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'net-tools: legacy network support'
            'wireless_tools: Wireless networking')
makedepends=(asciidoc)
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/${pkgname}-${pkgver}.tar.xz"
        '0001-remove-run-nologin-before-leaving-rc.multi.patch'
        'wireless.conf.d'
        'alarm.patch'
        'rc.conf')
md5sums=('34493c768b587ac59c6ee7e14f275c1d'
         '87f02d111e0c613dd9fc61a9613f9c92'
         '027576534885b8d5dded9be546057b12'
         'a314e2858690154344b3a5a754f23dd3'
         'c618e099f522f861e08a475f8960a6c2')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}/alarm.patch"
  patch -Np1 <"$srcdir/0001-remove-run-nologin-before-leaving-rc.multi.patch"
  cp "${srcdir}/rc.conf" .
  [ "${CARCH}" == "arm" ]    && sed -i 's/@TTY@/ttyS0/' inittab
  [ "${CARCH}" == "armv7h" ] && sed -i 's/@TTY@/ttyO2/' inittab

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless
}
