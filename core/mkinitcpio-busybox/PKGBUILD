# $Id: PKGBUILD 135587 2011-08-16 07:52:56Z thomas $
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - removed -mtune, changed -march to v7/v5 compat

plugrel=1

pkgname=mkinitcpio-busybox
pkgver=1.18.5
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
source=(http://busybox.net/downloads/busybox-${pkgver}.tar.bz2
        config)
sha256sums=('10954fcd5c48d8a262a3497b16227bf983a05658bf2bf661af2fdeca773f2fc0'
            '0961b4db5eb5eaa1b8d353a3fc792884f4f2733ec0f5715ba88a0e667f0f48ec')

build() {
  cd "${srcdir}/busybox-${pkgver}"

  unset CFLAGS CXXFLAGS
  if [ ${CARCH} = "armv7h" ]; then
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=armv7-a -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  else
    sed 's|^CONFIG_EXTRA_CFLAGS=.*|CONFIG_EXTRA_CFLAGS="-march=armv5te -Os -pipe -fno-strict-aliasing"|' "${srcdir}/config" > .config
  fi
  make
}

package() {
  install -D -m755 "${srcdir}/busybox-${pkgver}"/busybox "${pkgdir}/lib/initcpio/busybox"
}
# vim:set ts=2 sw=2 et:
