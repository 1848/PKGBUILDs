# $Id: PKGBUILD 141111 2011-10-24 09:59:20Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - removed -mtune, changed -march to v7/v5 compat

plugrel=1

pkgname=mkinitcpio-busybox
pkgver=1.19.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=("http://busybox.net/downloads/busybox-$pkgver.tar.bz2"
        'config')
sha256sums=('ea7ec9b6df70b8c528f4a2b6300e9913431c7223308fb08dfafa7508d75a0cb9'
            '21ae2cd21e33ba1ef9500a9b14273579fc554bf995cfd7907fb3fe94fc162cf8')

build() {
  cd "$srcdir/busybox-$pkgver"

  if [ ${CARCH} = "armv7h" ]; then
    ARMCARCH="armv7-a"
  else
    ARMCARCH="armv5te"
  fi

  local safeflags="${ARMCARCH} -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="-march='"$safeflags"'"|' \
    "$srcdir/config" > .config

  make
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/lib/initcpio/busybox"
}

# vim:set ts=2 sw=2 et:
