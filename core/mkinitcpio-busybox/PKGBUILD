# $Id: PKGBUILD 141111 2011-10-24 09:59:20Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - removed -mtune, changed -march to v7/v6/v5 compat

pkgname=mkinitcpio-busybox
pkgver=1.20.2
pkgrel=1
pkgdesc="base initramfs tools"
arch=('i686' 'x86_64')
url="http://www.busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=("http://busybox.net/downloads/busybox-$pkgver.tar.bz2"
        'glibc-2.16.patch'
        'config')
sha256sums=('eb13ff01dae5618ead2ef6f92ba879e9e0390f9583bd545d8789d27cf39b6882'
            'fb5b6e2a0de4db5401322e5c2474ad8ce6a58615dad45b7109cfe045baf2c88d'
            'd8064ed6ec21868e4afe057445e2d852b353abd595132cb1ca3ba345988772f0')

build() {
  cd "$srcdir/busybox-$pkgver"

  if [ ${CARCH} = "armv7h" ]; then
    ARMCARCH="armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16"
  elif [ ${CARCH} = "armv6h" ]; then
    ARMCARCH="armv6 -mfloat-abi=hard -mfpu=vfp"
  else
    ARMCARCH="armv5te"
  fi

  local safeflags="${ARMCARCH} -Os -pipe -fno-strict-aliasing"

  sed 's|^\(CONFIG_EXTRA_CFLAGS\)=.*|\1="-march='"$safeflags"'"|' \
    "$srcdir/config" > .config

  patch -Np1 < "$srcdir/glibc-2.16.patch"

  make
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}

# vim:set ts=2 sw=2 et:
