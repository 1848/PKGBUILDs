# $Id: PKGBUILD 148462 2012-02-01 13:16:32Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - fix for v5, credit: ambrop7 (issue #127)

plugrel=1

pkgname=kmod
pkgver=4
pkgrel=2.1
pkgdesc="Linux kernel module handling"
arch=('i686' 'x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git"
license=('GPL2')
depends=('glibc' 'zlib')
makedepends=('docbook2x')
options=('!libtool')
provides=('module-init-tools=3.16')
conflicts=('module-init-tools')
replaces=('module-init-tools')
source=("http://packages.profusion.mobi/$pkgname/$pkgname-$pkgver.tar.xz"
        0001-partially-fix-parsing-of-alias-with-dots.patch
        0002-libkmod-module-used-shared-code-in-module-creation.patch
        0003-modprobe-handle-all-error-returns-from-init_module.patch
        0004-modprobe-remove-0-refcnt-deps.patch
        v5-alignment-fix.patch
        "depmod-search.conf")
md5sums=('e14450a066a48accd0af1995b3c0232d'
         '5f497ab3466ee1a616b6e6c97b330706'
         '23a9257a152862753ce4c4ee7287761a'
         '3a57671b0f37b1203b207f35a4442ae3'
         '1fe88eee9302104b179124ce6bfc55d2'
         '99d003b8337d9372b043b0d449a6b59a'
         '4b8cbcbc54b9029c99fd730e257d4436')

build() {
  cd "$pkgname-$pkgver"

  patch -Np1 <"$srcdir/0001-partially-fix-parsing-of-alias-with-dots.patch"
  patch -Np1 <"$srcdir/0002-libkmod-module-used-shared-code-in-module-creation.patch"
  patch -Np1 <"$srcdir/0003-modprobe-handle-all-error-returns-from-init_module.patch"
  patch -Np1 <"$srcdir/0004-modprobe-remove-0-refcnt-deps.patch"
  [[ $CARCH == "arm" ]] && patch -p1 -i "${srcdir}/v5-alignment-fix.patch"

  ./configure \
    --sysconfdir=/etc \
    --with-rootprefix= \
    --with-zlib

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # binary directories
  install -dm755 "$pkgdir"/{,s}bin

  # configuration directories
  install -dm755 "$pkgdir"/{etc,lib}/{depmod,modprobe}.d

  # add symlinks to kmod
  ln -s /usr/bin/kmod "$pkgdir/bin/lsmod"
  for tool in {ins,rm,dep}mod mod{info,probe}; do
    ln -s ../usr/bin/kmod "$pkgdir/sbin/$tool"
  done

  # install depmod.d file for search/ dir
  install -Dm644 "$srcdir/depmod-search.conf" "$pkgdir/lib/depmod.d/search.conf"
}

# vim: ft=sh syn=sh et
