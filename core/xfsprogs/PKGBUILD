# $Id: PKGBUILD 142967 2011-11-19 08:45:35Z tpowa $
# Maintainer: Paul Mattal <paul@archlinux.org>

# ALARM: Kevin Mihelich <kevin@plugapps.com>
#  - original OPTIMIZER export does not play nice with our system

plugrel=1

pkgname=xfsprogs
pkgver=3.1.8
pkgrel=2
pkgdesc="XFS filesystem utilities"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('util-linux')
options=('!makeflags' '!libtool')
# We mirror the sources as upstream tends to move them once a new release is out
source=("ftp://ftp.archlinux.org/other/xfsprogs/${pkgname}-${pkgver}.tar.gz")
md5sums=('f70b2e7200d4c29f0af1cf70e7be1db6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export OPTIMIZER="-O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
