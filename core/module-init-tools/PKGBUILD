# $Id: PKGBUILD 144464 2011-12-06 10:57:38Z dreisner $
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - bringing back this package due to kmod not quite working for us yet
#  - this package replaces and provides kmod

plugrel=1

pkgname=module-init-tools
pkgver=3.16
pkgrel=5
pkgdesc="Utilities needed by Linux systems for managing loadable kernel modules"
arch=('i686' 'x86_64')
url="https://modules.wiki.kernel.org"
license=('GPL')
depends=('glibc')
makedepends=('docbook2x')
provides=('kmod=4')
replaces=('kmod')
backup=('etc/modprobe.d/modprobe.conf'
        'etc/depmod.d/depmod.conf')
source=("http://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.bz2"
        modprobe.conf
        depmod.conf
        docfix.patch)
md5sums=('bc44832c6e41707b8447e2847d2019f5'
         '325657db471b3190f685e5a89c9b90e3'
         '4b8cbcbc54b9029c99fd730e257d4436'
         '4320d19902ded43bed55c804b065cdc7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix headers in SGML manpage sources and an XML typo
  patch -Np0 < "$srcdir/docfix.patch"

  ./configure --prefix=/usr --exec-prefix=/ --enable-zlib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=$pkgdir install

  # extra modprobe.d and depmod.d conf dirs
  install -dm755 "$pkgdir/lib/modprobe.d" "$pkgdir/etc/depmod.d"

  # Install custom depmod config to allow searching extramodules/
  install -Dm644 "$srcdir/depmod.conf" "$pkgdir/etc/depmod.d/depmod.conf"

  # Install our custom (empty) modprobe.conf
  install -Dm644 "$srcdir/modprobe.conf" "$pkgdir/etc/modprobe.d/modprobe.conf"
}
