# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-config-general
_pkgname=Config-General
pkgver=2.51
pkgrel=1
pkgdesc="Config::General - Generic Config Module"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_pkgname"
source=("http://search.cpan.org/CPAN/authors/id/T/TL/TLINDEN/$_pkgname-$pkgver.tar.gz")
sha512sums=('71a0d607ab69b82aa6ff3eb74efe7d90b67859331ae3fe2d6d14829e71eedacdeb98506e38c6506b4b7400ed31cd20de09b5b8ef86fe6a23599677aa8e5f5982')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
