# Maintainer: Jason St. John <jstjohn .. purdue . edu>

plugrel=1

pkgname=perl-config-general
_pkgname=Config-General
pkgver=2.50
pkgrel=2
pkgdesc="Config::General - Generic Config Module"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/T/TL/TLINDEN/$_pkgname-$pkgver.tar.gz)
sha512sums=('d4c9beaf7b951e8dfe8198e95acf43a6e90e8a07f9fe8cb0c2fe14b6de6b6140c98b48ff3019454fe57340812684ab66490b1b12a99a349a1f51b27ea26ba46e')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
