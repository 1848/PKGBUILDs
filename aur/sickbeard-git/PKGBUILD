# Maintainer: sudokode <sudokode@gmail.com>>
# Previous Maintainer: Crass00 <crass00 at hotmail dot com>
# Previous Maintainer: Superstar655 <choman000 at hotmail dot com>
# Contributor: Augusto Born de Oliveira <augustoborn at gmail dot com>

pkgname=sickbeard-git
pkgver=20120724
pkgrel=1
pkgdesc="A PVR application that downloads and manages your TV shows"
arch=(any)
url="http://code.google.com/p/sickbeard/"
license=(GPL3)
depends=(python2 python2-cheetah)
makedepends=(git)
optdepends=('sabnzbd: NZB downloader'
				'python-notify: desktop notifications')
install=sickbeard.install
backup=('etc/conf.d/sickbeard')
conflicts=(sickbeard)
source=('sickbeard' 'sickbeard.init' 'sickbeard.confd' 'sickbeard.service' 'sickbeard.tmpfile')
md5sums=('67f346e3e217c38db3f5f9250a2f3fa2'
         '276747cb646524bbee02e1b1c7cd537c'
         'b9ab9c22027905338b8edda3531f48f2'
         '17964f6ca27e2fe7936888edd447d2d7'
         '08bbd75c6cf2ef3e4797d9221f0ce385')

_gitroot="git://github.com/midgetspy/Sick-Beard.git"
_gitname="sickbeardpvr"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ]; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
  else
     git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting install..."
}

package() {
  mkdir -p "${pkgdir}/opt/"
  cp -r "$srcdir/$_gitname" "${pkgdir}/opt/sickbeard"

  sed -i 's/python/python2/g' "${pkgdir}/opt/sickbeard/autoProcessTV/sabToSickBeard.py"
  sed -i 's/python/python2/g' "${pkgdir}/opt/sickbeard/autoProcessTV/hellaToSickBeard.py"

  install -D -m755 "${srcdir}/sickbeard" "${pkgdir}/usr/bin/sickbeard"
  install -D -m755 "${srcdir}/sickbeard.init" "${pkgdir}/etc/rc.d/sickbeard"
  install -D -m644 "${srcdir}/sickbeard.confd" "${pkgdir}/etc/conf.d/sickbeard"
  install -D -m644 "${srcdir}/sickbeard.service" "${pkgdir}/usr/lib/systemd/system/sickbeard.service"
  install -D -m644 "${srcdir}/sickbeard.tmpfile" "${pkgdir}/usr/lib/tmpfiles.d/sickbeard.conf"
}

