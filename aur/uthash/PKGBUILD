# Maintainer: Michael Frey <mail@mfrey.net>
pkgname=uthash
pkgver=1.9.6
pkgrel=1
pkgdesc="uthash provides C preprocessor implementations of a hash table and a linked list"
arch=('any')
url="http://uthash.sourceforge.net/"
license=('BSD')
source=(http://downloads.sourceforge.net/uthash/$pkgname-$pkgver.tar.bz2)
sha512sums=('ff03644b5e7cdf59a87e856052c84f68ee15c343b6d83a286f68a1b5aabed40e1f4bebc063602e0840954db391354c9078a29811a47aa901aac1cc23ab35018f')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
 
  # create directory for header files 
  install -dm755 "${pkgdir}"/usr/include/

  # install header files in /usr/include
  for h in $(ls -1 *.h); do
      install -m 644 ${h} "${pkgdir}"/usr/include/
  done

  # install licence file
  install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

