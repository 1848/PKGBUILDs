# Maintainer:  Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <`echo 'moc tod liamg ta nosaj tod ecreip' | rev`>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>

pkgname=dropbear
pkgver=2012.55
pkgrel=2
pkgdesc="Lightweight replacement for sshd"
arch=('i686' 'x86_64')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('MIT')
depends=('zlib')
source=(http://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2
        $pkgname.service)
sha256sums=('04982af2a10b220fa940f9f72f276d612c9bb643cfbb5ee1416e5a0f00de9b0f'
            'f0158ba3c98e9b26ca0ef1f5a1047d72677ba5011f2e2d2014a12e48c2af09bf')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed -i 's|usr/libexec/sftp|usr/lib/ssh/sftp|' options.h

  ./configure --prefix=/usr
  LIBS="-lcrypt" make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=${pkgdir}/usr install

 #man pages
  install -D -m644 dbclient.1 ${pkgdir}/usr/share/man/man1/dbclient.1
  install -D -m644 $pkgname.8 ${pkgdir}/usr/share/man/man8/$pkgname.8
  install -D -m644 dropbearkey.8 ${pkgdir}/usr/share/man/man8/dropbearkey.8

 #configuration files
  install -d ${pkgdir}/etc/$pkgname
  install -D -m644 ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service

 #license file
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
