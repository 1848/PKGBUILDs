# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <`echo 'moc tod liamg ta nosaj tod ecreip' | rev`>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

plugrel=1

pkgname=dropbear
pkgver=2011.55
pkgrel=1
pkgdesc="Lightweight replacement for sshd"
arch=('i686' 'x86_64')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('custom')
depends=('zlib')
backup=(etc/conf.d/dropbear)
source=(http://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2 \
        $pkgname-conf.d \
        $pkgname-rc.d)
sha256sums=('04982af2a10b220fa940f9f72f276d612c9bb643cfbb5ee1416e5a0f00de9b0f'
            'eaec8cecae4476a660351ba9079c91b7c379146a22df881bd24b81af8f50d74c'
            '29ab5438d15bfa727620fc04ec52ff55d676577b903c9261ce00bb291ae171a8')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed -i 's:usr/libexec/sftp:usr/lib/ssh/sftp:' options.h

  ./configure --prefix=/usr
  LIBS="-lcrypt" make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=${pkgdir}/usr install

#man pages
  install -D -m644 dbclient.1 ${pkgdir}/usr/share/man/man1/dbclient.1
  install -D -m644 $pkgname.8 ${pkgdir}/usr/share/man/man8/$pkgname.8
  install -D -m644 dropbearkey.8 ${pkgdir}/usr/share/man/man8/dropbearkey.8

#configuration files
  install -d ${pkgdir}/etc/$pkgname
  install -D -m644 ${srcdir}/$pkgname-conf.d ${pkgdir}/etc/conf.d/$pkgname
  install -D -m755 ${srcdir}/$pkgname-rc.d ${pkgdir}/etc/rc.d/$pkgname

#license file
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
