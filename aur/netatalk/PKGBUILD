# Maintainer: SJ_UnderWater
# Based on netatalk package :
# Maintainer: Dominik Dingel <mail at wodar dot de>
# Contributor: William Udovich <nerdzrule7 at earthlink dot net>
# Contributor: Farhan Yousaf <farhany at xaviya dot com>

pkgname=netatalk
pkgver=3.0
pkgrel=3
pkgdesc='A kernel-level implementation of AFP services'
arch=('i686' 'x86_64')
url='http://netatalk.sourceforge.net'
license=('GPL')
depends=('avahi>=0.6' 'libldap' 'libgcrypt>=1.2.3' 'libevent')
replaces=('netatalk-git' 'netatalk2')
backup=('etc/afp.conf')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(netatalk.rc http://iweb.dl.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('5e92015b0d9ac77af4b598cca6cb435c'
	'62eb034011bb60b0bfd95072af3693dc')

build() {
	cd $pkgname-$pkgver
	msg2 'Fixing...'
	sed -i 's/x"linux/x"generic/' macros/netatalk.m4
	autoreconf &>/dev/null
	echo -e "all:\ninstall:">libevent/Makefile
	msg2 'Configuring...'
	CFLAGS="-Wno-unused-result" LIBS=-levent ./configure --prefix=/usr --localstatedir=/etc --sysconfdir=/etc \
		--disable-bundled-libevent --with-cracklib --with-cnid-cdb-backend --enable-pgp-uam
	msg2 'Making...'
	make >/dev/null
}
package() {
	cd $pkgname-$pkgver
	msg2 'Building...'
	make DESTDIR="$pkgdir" install >/dev/null
	msg2 'Copying initscripts...'
	install -D -m755 "$srcdir"/netatalk.rc "$pkgdir"/etc/rc.d/netatalk
}
