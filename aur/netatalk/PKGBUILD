# Maintainer: Dominik Dingel <mail at wodar dot de>
# Contributor: William Udovich <nerdzrule7 at earthlink dot net>
# Contributor: Farhan Yousaf <farhany at xaviya dot com>

plugrel=1

pkgname=netatalk
pkgver=2.2.1
pkgrel=1
pkgdesc="A kernel level implementation of the AppleTalk Protocol Suite"
arch=('i686' 'x86_64')
url="http://netatalk.sourceforge.net"
options=('!libtool')
license=("GPL")
backup=('etc/netatalk/afpd.conf' 
        'etc/netatalk/netatalk.conf' 
        'etc/netatalk/atalkd.conf' 
        'etc/netatalk/papd.conf' 
        'etc/netatalk/AppleVolumes.default' 
        'etc/netatalk/AppleVolumes.system'
        'etc/avahi/services/afpd.service')
depends=('libcups' 'tcp_wrappers' 'avahi' 'openssl' 'pam' 'coreutils>=7.1-2' 'db')
makedepends=('make' 'patch' 'gcc')
source=(http://voxel.dl.sourceforge.net/project/netatalk/netatalk/2.2.1/netatalk-$pkgver.tar.bz2 \
afpd atalkd papd cnid afpd.service)
install=netatalk.install
md5sums=('4a371025daf96adb73fa491efdbfef28'
         '16ab9fa50ec4abde6de478fc7de57805'
         '2d05de4a16faf7d4af21b5f14e33fa82'
         'b16a687c96dd1ca7ffefd7c995356c0d'
         '84d1961726aaa8df08d63a0925358b1a'
         '9b6b2fee54fe052bba0c69f00d335bdb')

build() {
	cd "${srcdir}/netatalk-${pkgver}"
	./configure --prefix=/usr --with-cnid-cdb-backend --with-ssl-dir=/usr --localstatedir=/var --enable-fhs --enable-zeroconf=/usr --disable-srvloc --without-xfs
	make || return 1
}

package() {
	cd "${srcdir}/netatalk-${pkgver}"
	make DESTDIR=${pkgdir} install

	mv ${pkgdir}/usr/include/netatalk{,2}

	install -d ${pkgdir}/etc/rc.d
	install -m755 ../{afpd,atalkd,papd,cnid} ${pkgdir}/etc/rc.d
   
    install -d ${pkgdir}/etc/avahi/services
    install -m755 ../afpd.service ${pkgdir}/etc/avahi/services


#	rm $startdir/pkg/usr/bin/timeout
	rm -f ${pkgdir}/usr/share/man/man1/timeout.1{,.gz}
}
