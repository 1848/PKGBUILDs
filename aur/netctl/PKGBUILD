# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl
pkgver=0.3
pkgrel=2
arch=(any)
pkgdesc="Network configuration and profile scripts"
url="http://projects.archlinux.org/netctl.git/"
license=("GPL")
depends=("coreutils" "iproute2" "openresolv")
makedepends=('asciidoc')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support, or use dhcpcd'
            'dhcpcd: for DHCP support, or use dhclient'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
            )
conflicts=("netcfg")
provides=("netcfg")


source=("https://projects.archlinux.org/$pkgname.git/snapshot/$pkgver.tar.gz"
        'zsh-completion')

package() {
  make -C "$srcdir/$pkgname-$pkgver" DESTDIR="$pkgdir" install
  # Shell Completion
  install -D -m644 "$srcdir/$pkgname-$pkgver/contrib/bash-completion" "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -Dm644 "$srcdir/zsh-completion" "$pkgdir/usr/share/zsh/site-functions/_netctl"
}

md5sums=('ec67fcb75fdb750847a9ebfcd10806df'
         '3428e0e7f061bbcde41f2fe64d8d96dd')
