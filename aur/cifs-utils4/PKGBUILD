# $Id: PKGBUILD 176097 2013-01-27 12:26:02Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

#ALARM: moonman
#    -cifs-utils depends on libwbclient4 from samba4
#    -but smbclient4 from samba4 depends on cifs-utils
#    -hence the introduction of this pacakge.
#
#    - changed the name to cifs-utils4
#    - introduced new variable _pkgbase

pkgname=cifs-utils4
_pkgbase=cifs-utils
pkgver=6.0
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(arm)
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient4')
source=(ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$_pkgbase-$pkgver.tar.bz2)
conflicts=('cifs-utils')
provides=("cifs-utils=${pkgver}")

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  # systemd support is broken in mount.cifs
  # https://bugs.archlinux.org/task/30958
  ./configure --prefix=/usr --disable-systemd
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s $pkgdir/usr/bin/mount.cifs
}
md5sums=('371e007a201be90c16497cd9bd5e2553')
