# $Id: PKGBUILD 176097 2013-01-27 12:26:02Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

#ALARM: moonman
#    -cifs-utils depends on libwbclient4 from samba4
#    -but smbclient4 from samba4 depends on cifs-utils
#    -hence the introduction of this pacakge.
#
#    - changed the name to cifs-utils4
#    - introduced new variable _pkgbase

pkgname=cifs-utils4
_pkgbase=cifs-utils
pkgver=5.9
pkgrel=1
pkgdesc="CIFS filesystem user-space tools"
arch=(arm)
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient4')
source=(ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$_pkgbase-$pkgver.tar.bz2
        fix-5.9-credentials.patch)
conflicts=('cifs-utils')


build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  # fix credentials
  patch -Np1 -i ../fix-5.9-credentials.patch
  # systemd support is broken in mount.cifs
  # https://bugs.archlinux.org/task/30958
  ./configure --prefix=/usr --disable-systemd
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s $pkgdir/usr/bin/mount.cifs
}
md5sums=('6aa9c4347780872fefe5823ac06a8ff3')
md5sums=('7164ad6f7963a31fcbffbe4f14a7cfc6'
         '2550d43af5bb734307cae22a09cd7710')
