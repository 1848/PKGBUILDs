# Maintainer: Mitch Bigelow <ipha00@gmail.com>

plugrel=1

pkgname=pianobar
pkgver=2012.04.24
pkgrel=2
pkgdesc="console-based frontend for Pandora (official version)"
url="http://6xq.net/0017"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libao' 'faad2' 'libmad' 'gnutls')
source=(http://6xq.net/media/00/16/pianobar-$pkgver.tar.bz2
		protocol-fix.patch)
sha256sums=('1286005b14ef58ea5b7baece054e772761e6351fdeceadd3a69e0a7c8b9bf28e'
			'39f30666783d28ccb0bdfe985738bb06c9545e6f0f42e3eba905da2d202de2f7')
_builddir="$pkgname-$pkgver"

build() {
    cd "$_builddir"

    # temporary fix from https://gist.github.com/2500616/654f1869da5698696ce60cb8a7fe3cae0f4dfd25
    patch -p 1 -i ../protocol-fix.patch

    make
}

package() {
    cd "$_builddir"

    make DESTDIR=$pkgdir PREFIX=/usr install
    install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
