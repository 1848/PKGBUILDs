# Maintainer:   Huulivoide      <gmail.com: jesse.jaara>
# Contributor:  Jonathan Hudson <daria.co.uk: jh+arch>

buildarch=18

pkgname=omxplayer-git
pkgver=284.583f016
pkgrel=1
pkgdesc="omxplayer is a command line media player for the RaspberryPi"
arch=('arm' 'armv6h')
url="https://github.com/popcornmix/omxplayer"
license=(GPL2)
depends=(ffmpeg raspberrypi-firmware-tools fbset dbus)
makedepends=(git boost)
optdepends=(ttf-freefont)
provides=(omxplayer)
conflicts=(omxplayer omxplayer-bin)
source=(git://github.com/popcornmix/omxplayer.git
        omxplayer.arch Makefile.arch Makefile.include.arch)
md5sums=('SKIP'
         'b256bd3a81a974543a0afc62b62e5c28'
         '462e40e8da2e2b10cb32138e7885d285'
         'a8635442d02665944c04c18606453a04')

pkgver() {
  cd omxplayer
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/omxplayer"

  cp ../Makefile.arch Makefile
  cp ../Makefile.include.arch Makefile.include
  cp ../omxplayer.arch omxplayer
}

build() {
  cd "${srcdir}/omxplayer"

  make
}

package() {
  cd "${srcdir}/omxplayer"

  make DESTDIR="${pkgdir}" install
}
